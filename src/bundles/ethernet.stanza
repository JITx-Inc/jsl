#use-added-syntax(jitx)
defpackage jsl/bundles/ethernet:
  import core
  import jitx
  import jitx/commands

  import jsl/bundles/general


doc: \<DOC>
Reduced Gigabit Media Independent Interface (RGMII)
@see https://en.wikipedia.org/wiki/Media-independent_interface#RGMII
<DOC>
public pcb-bundle rgmii :
  name = "RGMII"
  port txd : pin[4]
  port rxd : pin[4]

  pin tx-clk
  pin tx-ctl
  pin rx-clk
  pin rx-ctl

doc: \<DOC>
Media Independent Interface Management (MIIM)
This is the configuration interface for the PHY or switch.
<DOC>
public pcb-bundle miim :
  name = "MIIM"
  pin mdc
  pin mdio

public pcb-enum jsl/bundles/ethernet/MIIPins :
  MII-COL ; Collision
  MII-CS ; Carrier Sense
  MII-TXER ; Transmit Error

defn inject-optional-mii-pins (pins:Collection<MIIPins>) :
  inside pcb-bundle:
    for p in pins do :
      switch(p) :
        MII-COL: make-pin(`col)
        MII-CS: make-pin(`cs)
        MII-TXER: make-pin(`tx-er)

pcb-bundle gmii-b (pins:Collection<MIIPins>):
  name = "GMII"
  port txd : pin[8]
  port rxd : pin[8]

  pin gtx-clk
  pin tx-clk
  pin tx-en

  pin rx-clk
  pin rx-dv
  pin rx-er

  inject-optional-mii-pins(pins)

doc: \<DOC>
Gigabit Media Independent Interface (GMII) Bundle Generator
@see https://en.wikipedia.org/wiki/Media-independent_interface#GMII

@param pins Optional pins for:
*  `col` - Collision
*  `cs` - Carrier Sense
*  `tx-er` - Transmit Error
<DOC>
public defn gmii (pins:MIIPins ...) :
  gmii-b(pins)

pcb-bundle mii-b (pins:Collection<MIIPins>):
  name = "MII"
  port txd : pin[4]
  port rxd : pin[4]

  pin tx-clk
  pin tx-en

  pin rx-clk
  pin rx-dv
  pin rx-er

  inject-optional-mii-pins(pins)

doc: \<DOC>
Media Independent Interface (MII) Bundle Generator
@see https://en.wikipedia.org/wiki/Media-independent_interface#Standard_MII

@param pins Optional pins for:
*  `col` - Collision
*  `cs` - Carrier Sense
*  `tx-er` - Transmit Error
<DOC>
public defn mii (pins:MIIPins ...) :
  mii-b(pins)

pcb-bundle rmii-b (add-rx-error:True|False):
  name = "RMII"
  port txd : pin[2]
  port rxd : pin[2]

  pin ref-clk

  pin tx-en
  pin crs-dv

  if add-rx-error:
    pin rx-er

doc: \<DOC>
Reduced Medium Independent Interface (RMII) Bundle Generator
@see https://en.wikipedia.org/wiki/Media-independent_interface#Reduced_media-independent_interface
@param add-rx-error This flag will add the optional `rx-er` pin to the bundle
<DOC>
public defn rmii (add-rx-error:True|False = true):
  rmii-b(add-rx-error)