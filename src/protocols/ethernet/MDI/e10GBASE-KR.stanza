doc: \<DOC>
@brief 10GBASE-KR Protocol

10GBASE-KR is a ethernet communication supporting high speed data links
in copper for backplane connections
@see https://en.wikipedia.org/wiki/10_Gigabit_Ethernet#10GBASE-KR

This functions and definitions in this file support defining 10GBASE-KR
connections between components and/or connectors in a board design.

## 10GBASE-KR Blocking Capacitors

The 10GBASE-KR specification calls for AC coupling for the data lanes. This is typically
achieved using a blocking capacitor. When connecting two active components, this typically
means blocking caps from `Tx -> Rx` on both sides of the link. When connecting an
active component to a passive component, this typically means adding the blocking caps only on
the `Tx -> Rx` side of the link.

The functions in this module allow you to pass a blocking capacitor as an `Instantiable`.
This component will get instantiated for each leg of the diff-pair. These functions handle
the topology configuration, but the user needs to set a `pin-model` statement on the capacitor component.


<DOC>
#use-added-syntax(jitx)
defpackage jsl/protocols/ethernet/MDI/e10GBASE-KR:
  import core
  import jitx
  import jitx/commands

  import jsl/bundles
  import jsl/errors
  import jsl/si/helpers
  import jsl/si/couplers
  import jsl/si/pairs
  import jsl/pin-assignment
  import jsl/si/constraints

doc: \<DOC>
@brief e10GBASE-KR Bundle
The Data Bundle consists of a number (1 or more) of RX and TX pairs (1 RX/TX pair set = 1 lane)
@param num-lanes The number of lanes to be configured for this bundle.
<DOC>
public pcb-bundle e10GBASE-KR (num-lanes:Int) :
  name = "10GBASE-KR"
  description = "10GBASE-KR Ethernet Backplane Communications Link"
  port lane : lane-pair[num-lanes]

doc: \<DOC>
@brief Construct the 10GBASE-KR Topology and Constraints

This function constructs the 10GBASE-KR topology and applies constraints
to the channel for intra-pair skew and channel loss.

@param cons The Constraint structure to be used for the routing.
@param cap-src-dst Optional DC blocking capacitor to be inserted between the src.TX and dst.RX signal ends. Note that
there are 2 blocking caps inserted, one on each of the pair of P,N signals. The capacitor needs to have pin models applied to its pins
in order for the overall skew and loss values to be respected.
@param cap-dst-src Optional DC blocking capacitor to be inserted between the dst.TX and src.RX signal ends. This capacitor
covers the reverse direction connection between dst.TX and src.RX. If the second component was a connector, then typically
no capacitor is inserted between dst.TX and src.RX (cap-dst-src = false).
@param src This is expected to be a port of `Bundle` type `e10GBASE-KR`
@param dst This is expected to be a port of `Bundle` type `e10GBASE-KR`
@param null-modem If set to true, connects src.TX through the optional cap-src-dst to dst.RX and dst.TX through the optional cap-dst-src to src.RX. 
If set to false, connects src.TX through the optional cap-src-dst to dst.TX and dst.RX through the optional cap-dst-src to src.RX
<DOC>
public defn connect-10GBASE-KR (cons:SI-Constraint, cap-src-dst:Instantiable|False, cap-dst-src:Instantiable|False, src:JITXObject, dst:JITXObject, -- null-modem:True|False = false) :
  inside pcb-module :
    val lane-constraint = LaneConstraint(cons)
    if length(src.lane) != length(dst.lane) :
      throw $ Exception("Invalid Port Widths - Different Widths for src/dst")
    for i in indices(src.lane) do :
      match(null-modem, cap-src-dst, cap-dst-src) :
        (nm:True|False, x:False, y:False) :
          val dst-r = if nm : reverse-lane(dst) else : dst
          within [s,d] = constrain-topology(src.lane[i] => dst-r.lane[i], lane-constraint) :
            topo-net(s.TX, d.TX)
            topo-net(s.RX, d.RX)
        (nm:True, csd:Instantiable, cds:Instantiable) :
          within [s,d] = constrain-topology(src.lane[i] => reverse-lane(dst.lane[i]), lane-constraint) :
            inst bl-cap-xy : dp-coupler(csd)
            inst bl-cap-yx : dp-coupler(cds)
            topo-pair(s.TX => bl-cap-xy => d.TX)
            topo-pair(d.RX => bl-cap-yx => s.RX)
        (nm:True|False, csd:Instantiable, y:False) :
          val dst-r = if nm : reverse-lane(dst) else : dst
          within [s,d] = constrain-topology(src.lane[i] => dst-r.lane[i], lane-constraint) :
            inst bl-cap-xy : dp-coupler(csd)
            topo-pair(s.TX => bl-cap-xy => d.TX)
            topo-net(s.RX, d.RX)
        (nm:True|False, x:False, cds:Instantiable) :
          val src-r = if nm : reverse-lane(src) else : src
          within [s,d] = constrain-topology(dst.lane[i] => src-r.lane[i], lane-constraint) :
            inst bl-cap-yx : dp-coupler(cds)
            topo-pair(d.TX => bl-cap-yx => s.TX)
            topo-net(d.RX, s.RX)
        (x, y, z) :
          throw $ Exception("Invalid Configuration - null-modem and/or blocking caps conflict")
      
doc: \<DOC>
@brief Curated values for skew and loss of 10GBASE-KR Channel
This is a helper function that returns the bounds on the intra-pair
skew timing and maximum loss as expected by the particular standard targeted by
the user.The values returned are a toleranced value with upper/lower limits for the
intra-pair skew and the maximum loss as a double representing dB. Some defaults in the
table are derived from the reference listed below.
https://www.ieee802.org/3/ba/public/jul08/balasubramanian_01_0708.pdf


Calculating the intra-pair skew distance to time correspondence depends on the material.
see @http://pdf.cloud.opensystemsmedia.com/advancedtca-systems.com/Simclar.Feb08.pdf
Table 3 where the intra-pair skew is set to 0.0625e-12 and the inter-pair skew
is set to 0.625e-12. This corresponds roughly to 0.01mm skew intra-pair and 
0.100mm skew inter-pair (or lane).
<DOC>

public defn e10GBASE-KR-get-skew-loss-vals () -> [Toleranced, Toleranced, Double]:
    [0.0 +/- 0.0625e-12, 0.0 +/- 0.625e-12, 15.0]

doc: \<DOC>
@brief Differential impedance specified by the e10GBASE-KR standard
This is a helper function that returns the expected differential
trace impedance for the specified generation of the e10GBASE-KR standard.

@return Upper/lower limits for the impedance.
<DOC>

public defn e10GBASE-KR-get-trace-impedance () -> Toleranced :
  100.0 +/- (10 %)  ;   2.5 GT/s


doc: \<DOC>
Construct a e10GBASE-KR-Constraints object

@param route-struct Differential Pair Routing Structure - Use
`pcb-differential-routing-structure` to create.
@return Diff-Pair Constraint Object
<DOC>
public defn e10GBASE-KR-Constraint (
  --
  route-struct:DifferentialRoutingStructure
  ) -> DP-Constraint :
  val [s, c, ml] = e10GBASE-KR-get-skew-loss-vals()
  DP-Constraint(skew = s, loss = ml, route-struct = route-struct)

