#use-added-syntax(jitx)
defpackage jsl/landpatterns/BGA/planner:
  import core
  import jitx
  import jitx/commands

  import jsl/landpatterns/BGA/pads
  import jsl/landpatterns/pad-planner
  import jsl/landpatterns/pad-island
  import jsl/landpatterns/grid-planner
  import jsl/geometry/basics
  import jsl/ensure

val DEF-PAD-CONFIG = PadConfig-R()

doc: \<DOC>
@title BGA-PadPlanner
@desc This pad planner will create circular pads for each BGA
ball location. The user can use the `pad-config` to
modify the construction of each pad including the soldermask
and pastemask openings.

This type is derived from two parent implementations:
1.  {@link type:PadPlanner} which manages selection of which pads to populate
and how they should be constructed
2.  {@link type:GridPlanner} which manages the placement of the created pads
at particular grid locations.
@member pad-config Defines the features of each individual BGA pad.
@member pose Pose of the landpattern.
<DOC>
public defstruct BGA-PadPlanner <: PadPlanner & GridPlanner:
  doc: \<DOC>
  @title pad-config
  @desc Defines the features of each individual BGA pad.
  This defines the shape and parameters for the copper,
  soldermask opening, paste application, etc.
  <DOC>
  pad-config:PadConfig with:
    default => DEF-PAD-CONFIG

  ; GridPlanner Base
  doc: \<DOC>
  @title pose
  @desc Pose of the landpattern (default is loc(0.0, 0.0))
  <DOC>
  pose:Pose with:
    as-method => true
    default => loc(0.0, 0.0)

doc: \<DOC>
@title BGA-Pad-Shaper
@desc Default Pad Shaper for BGA packages.
This accepts a `Dims` object and will convert
it to a `Circle` using just the `x` dimension of the `Dims`.
@param s Dimension object
<DOC>
public defn BGA-Pad-Shaper (s:Dims) -> Shape :
  Circle(to-radius $ x(s))

doc: \<DOC>
@title shape-generator
@desc Default Pad Shaper for BGA packages.
@param bga BGA Pad Planner
@param row Row of the landpattern
@param column Column of the landpattern
<DOC>
public defmethod shape-generator (bga:BGA-PadPlanner, row:Int, column:Int) -> (Dims -> Shape)|False :
  BGA-Pad-Shaper

doc: \<DOC>
@title pad-generator
@desc Default Pad Generator for BGA packages.
This accepts a `BGA-PadPlanner` and will generate a `Pad`
using the `shape-generator` function.
@param bga BGA Pad Planner
@param row Row of the landpattern
@param column Column of the landpattern
<DOC>
public defmethod pad-generator (x:BGA-PadPlanner, row:Int, column:Int) -> (Dims -> Pad)|False :
  val func? = shape-generator(x, row, column)
  match(active?(x, row, column), func?):
    (active:False, x): false
    (active:True, f:(Dims -> Shape)):
      build-bga-pad{f(_0), pad-config(x)}
    (active:True, f:False):
      throw $ NoGeneratorForActivePadError(row, column, "shape-generator")


doc: \<DOC>
@title Full-Matrix-Planner
@desc Full Matrix - All Pads in the grid are active
@member pad-config Defines the features of each individual BGA pad.
@member rows Number of rows in the landpattern.
@member columns Number of columns in the landpattern.
@member pitch Pitch of the landpattern.
@member pose Pose of the landpattern.

@snippet Example of a Full Matrix BGA pad planner
```stanza
  ; NSMD
  val rows = 3
  val cols = 3
  val grid = Grid-Numbering(rows, cols)
  val pitch = 0.5

  val planner = Full-Matrix-Planner(
    rows = rows
    columns = cols
    pitch = pitch
  )
```
<DOC>
public defstruct Full-Matrix-Planner <: BGA-PadPlanner :
  doc: \<DOC>
  @title pad-config
  @desc Defines the features of each individual BGA pad.
  This defines the shape and parameters for the copper,
  soldermask opening, paste application, etc.
  <DOC>
  pad-config:PadConfig with:
    as-method => true
    default => DEF-PAD-CONFIG

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; GridPlanner
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  doc: \<DOC>
  @title rows
  @desc Number of rows in the landpattern (should be a positive number).
  <DOC>
  rows:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title columns
  @desc Number of columns in the landpattern (should be a positive number).
  <DOC>
  columns:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pitch
  @desc Pitch of the landpattern (should be a positive number).
  <DOC>
  pitch:Double|Dims with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pose
  @desc Pose of the landpattern (default is loc(0.0, 0.0))
  <DOC>
    pose:Pose with:
    as-method => true
    default => loc(0.0, 0.0)
with:
  keyword-constructor => true
  printer => true

doc: \<DOC>
@title active?
@desc Full Matrix - Query if a pad is active
@param x Full-Matrix-Planner
@param row Row of the landpattern
@param column Column of the landpattern
<DOC>
public defmethod active? (x:Full-Matrix-Planner, row:Int, column:Int) -> True|False:
  true

doc: \<DOC>
@title Staggered-Matrix-Planner
@desc Staggered Matrix - Every other pad is in-active.

TODO - Diagram here

This pad planner assumes that the grid is structured
in a staggered pitch pattern.

@member phase Phase of the Staggered Matrix
@member pad-config Defines the features of each individual BGA pad.
@member rows Number of rows in the landpattern.
@member columns Number of columns in the landpattern.
@member pitch Pitch of the landpattern.
@member pose Pose of the landpattern.
<DOC>
public defstruct Staggered-Matrix-Planner <: BGA-PadPlanner :
  doc: \<DOC>
  @title phase
  @desc Phase indicates whether the first row pad 0 is empty or populated.
  By default this function uses `Even-Phase`
  <DOC>
  phase:StaggerPhase with:
    default => Even-Phase

  doc: \<DOC>
  @title pad-config
  @desc Defines the features of each individual BGA pad.
  This defines the shape and parameters for the copper,
  soldermask opening, paste application, etc.
  <DOC>
  pad-config:PadConfig with:
    as-method => true
    default => DEF-PAD-CONFIG

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; GridPlanner
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  doc: \<DOC>
  @title rows
  @desc Number of rows in the landpattern (should be a positive number).
  <DOC>
  rows:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title columns
  @desc Columns in the Staggered Matrix Grid.
  Note that there are typically 2x as many columns in
  the grid as pads that will get populated. Should be a positive number.
  <DOC>
  columns:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pitch
  @desc Pitch of the landpattern (should be a positive number).
  <DOC>
  pitch:Double|Dims with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pose
  @desc Pose of the landpattern (default is loc(0.0, 0.0))
  <DOC>
  pose:Pose with:
    as-method => true
    default => loc(0.0, 0.0)

with:
  keyword-constructor => true
  printer => true

doc: \<DOC>
@title active?
@desc Staggered Matrix - Query if a pad is active
@param x Staggered-Matrix-Planner
@param row Row of the landpattern
@param column Column of the landpattern
<DOC>
public defmethod active? (x:Staggered-Matrix-Planner, row:Int, column:Int) -> True|False:
  stagger-pattern(phase(x), row, column)

doc: \<DOC>
@title EquilateralTriangle-Matrix-Planner
@desc Equilateral Triangle Matrix - Similar to Staggered but all pads are equidistant.

TODO - Diagram

@member phase Phase of the Staggered Matrix
@member pad-config Defines the features of each individual BGA pad.
@member rows Number of rows in the landpattern.
@member columns Number of columns in the landpattern.
@member pitch Pitch of the landpattern.
@member pose Pose of the landpattern.
<DOC>
public defstruct EquilateralTriangle-Matrix-Planner <: Staggered-Matrix-Planner :
  doc: \<DOC>
  @title phase
  @desc See Staggered Matrix
  <DOC>
  phase:StaggerPhase with:
    as-method => true,
    default => Even-Phase

  doc: \<DOC>
  @title pad-config
  @desc Defines the features of each individual BGA pad.
  This defines the shape and parameters for the copper,
  soldermask opening, paste application, etc.
  <DOC>
  pad-config:PadConfig with:
    as-method => true
    default => DEF-PAD-CONFIG

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; GridPlanner
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  doc: \<DOC>
  @title rows
  @desc Number of rows in the landpattern (should be a positive number).
  <DOC>
  rows:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title columns
  @desc Columns in the Equilateral Triangle Grid.
  Note that there are typically 2x as many columns in
  the grid as pads that will get populated. Should be a positive number.
  <DOC>
  columns:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pitch
  @desc Pitch of the landpattern (should be a positive number).
  <DOC>
  pitch:Double|Dims with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pose
  @desc Pose of the landpattern (default is loc(0.0, 0.0))
  <DOC>
  pose:Pose with:
    as-method => true
    default => loc(0.0, 0.0)
  doc: \<DOC>
  @title tri-pitch
  @desc Equilateral Distance between triangle points
  All pads in this planner are equidistant apart and
  this parameter sets that distance. Is expected to be positive.
  <DOC>
  tri-pitch:Double with:
    ensure => ensure-positive!
with:
  keyword-constructor => true
  printer => true

defmethod pitch (g:EquilateralTriangle-Matrix-Planner) -> Double|Dims :
  val p = tri-pitch(g)
  equilateral-triangle-pitch(p)

doc: \<DOC>
@title Perimeter-Matrix-Planner
@desc Perimeter Matrix - Full matrix with an island of pads marked as inactive.

TODO - Diagram

@member inactive Island of Pads that will be marked inactive.
@member pad-config Defines the features of each individual BGA pad.
@member rows Number of rows in the landpattern.
@member columns Number of columns in the landpattern.
@member pitch Pitch of the landpattern.
@member pose Pose of the landpattern.
@snippet Example of a Perimeter Matrix BGA pad planner
```stanza
  ; NSMD
  val rows = 3
  val cols = 3
  val pitch = 0.5
  val grid = Grid-Numbering(rows, cols)
  
  val planner = Perimeter-Matrix-Planner(
    ; Pad A2 is not present
    inactive = to-pad-island(grid, "A[2]"),
    pad-config = PadConfig-D(
      copper-D-adj = ipc-bga-copper(
        NonSolderMaskDefined,
        density-level = DensityLevelC
        ),
      mask-D-adj = 0.0,
    ),
    rows = rows
    columns = cols
    pitch = pitch
  )
```
<DOC>
public defstruct Perimeter-Matrix-Planner <: BGA-PadPlanner :
  doc: \<DOC>
  @title inactive
  @desc Island of Pads that will be marked inactive.
  Typically, this will be an interior region near
  the center of the device.
  This argument can be either a single `PadIsland` or a
  `Tuple<PadIsland>`. In the `Tuple` case, the union of
  all `PadIsland`s in the tuple forms the inactive region.
  <DOC>
  inactive:PadIsland|Tuple<PadIsland>

  doc: \<DOC>
  @title pad-config
  @desc Defines the features of each individual BGA pad.
  This defines the shape and parameters for the copper,
  soldermask opening, paste application, etc.
  <DOC>
  pad-config:PadConfig with:
    as-method => true
    default => DEF-PAD-CONFIG

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; GridPlanner
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  doc: \<DOC>
  @title rows
  @desc Number of rows in the landpattern (should be a positive number).
  <DOC>
  rows:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title columns
  @desc Number of columns in the landpattern (should be a positive number).
  <DOC>
  columns:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pitch
  @desc Pitch of the pads in landpattern (should be a positive number).
  <DOC>
  pitch:Double|Dims with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pose
  @desc Pose of the landpattern pads (default is loc(0.0, 0.0))
  <DOC>
  pose:Pose with:
    as-method => true
    default => loc(0.0, 0.0)

with:
  keyword-constructor => true
  printer => true

defn not-inactive? (inactive:PadIsland|Tuple<PadIsland>, row:Int, column:Int) -> True|False :
  match(inactive):
    (island:PadIsland):
      not in-range?(island, row, column)
    (islands:Tuple<PadIsland>):
      for island in islands all?:
        not in-range?(island, row, column)

doc: \<DOC>
@title active?
@desc Perimeter Matrix - Query if a pad is active
@param p Perimeter-Matrix-Planner
@param row Row of the landpattern
@param column Column of the landpattern
<DOC>
public defmethod active? (p:Perimeter-Matrix-Planner, row:Int, column:Int) -> True|False :
  not-inactive?(inactive(p), row, column)

doc: \<DOC>
@title ThermallyEnhanced-Matrix-Planner
@desc Thermally Enhanced Matrix - Matrix with pad(s) marked for thermal features.

TODO - Diagram

@member inactive Island of Pads that will be marked inactive.
@member pad-config Defines the features of each individual BGA pad.
@member rows Number of rows in the landpattern
@member columns Number of columns in the landpattern
@member pitch Pitch of the landpattern
@member pose Pose of the landpattern
<DOC>
public defstruct ThermallyEnhanced-Matrix-Planner <: Perimeter-Matrix-Planner :
  doc: \<DOC>
  @title inactive
  @desc Island of Pads that will be marked inactive.
  Typically, this will be an interior region near
  the center of the device.
  This argument can be either a single `PadIsland` or a
  `Tuple<PadIsland>`. In the `Tuple` case, the union of
  all `PadIsland`s in the tuple forms the inactive region.
  <DOC>
  inactive:PadIsland|Tuple<PadIsland> with:
    as-method => true
  doc: \<DOC>
  @title active
  @desc Island of pads that will be marked active.
  This island will override the `inactive` island.
  This region is typically an interior region near
  the center of the device and enclosed by the `inactive` island.

  This argument can be either a single `PadIsland` or a
  `Tuple<PadIsland>`. In the `Tuple` case, the union of
  all `PadIsland`s in the tuple forms the active center region.
  <DOC>
  active:PadIsland|Tuple<PadIsland>

  doc: \<DOC>
  @title pad-config
  @desc Defines the features of each individual BGA pad.
  This defines the shape and parameters for the copper,
  soldermask opening, paste application, etc.
  <DOC>
  pad-config:PadConfig with:
    as-method => true
    default => DEF-PAD-CONFIG

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; GridPlanner
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  doc: \<DOC>
  @title rows
  @desc Number of rows in the landpattern (should be a positive number).
  <DOC>
  rows:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title columns
  @desc Number of columns in the landpattern (should be a positive number).
  <DOC>
  columns:Int with:
    ensure => ensure-positive!
    as-method => true
  doc: \<DOC>
  @title pitch
  @desc Pitch of the pads in landpattern (should be a positive number).
  <DOC>
  pitch:Double|Dims with:
    ensure => ensure-positive!
    as-method => true
  doc: \<DOC>
  @title pose
  @desc Pose of the landpattern pads (default is loc(0.0, 0.0))
  <DOC>
  pose:Pose with:
    as-method => true
    default => loc(0.0, 0.0)

with:
  keyword-constructor => true
  printer => true


defn is-active? (active:PadIsland|Tuple<PadIsland>, row:Int, column:Int) -> True|False :
  match(active):
    (island:PadIsland):
      in-range?(island, row, column)
    (islands:Tuple<PadIsland>):
      for island in islands any?:
        in-range?(island, row, column)

doc: \<DOC>
@title active?
@desc Thermally Enhanced Matrix - Query if a pad is active
@param p ThermallyEnhanced-Matrix-Planner
@param row Row of the landpattern
@param column Column of the landpattern
<DOC>
public defmethod active? (p:ThermallyEnhanced-Matrix-Planner, row:Int, column:Int) -> True|False :
  is-active?(active(p), row, column) or
    not-inactive?(inactive(p), row, column)

doc: \<DOC>
@title Corner-Cut-Matrix-Planner
@desc Corner Cut Matrix - A triangle of pads in each corner are inactive

Example: corner-cut = 2
  ```
    X | X | O  ...  O | X | X
    X | O | O  ...  O | O | X
    O | O | O  ...  O | O | O
       ...     ...     ...
    O | O | O  ...  O | O | O
    X | O | O  ...  O | O | X
    X | X | O  ...  O | X | X
  ```

@member corner-cut Integer count of the triangular region in each corner that will be inactive
@member pad-config Defines the features of each individual BGA pad.
@member rows Number of rows in the landpattern
@member columns Number of columns in the landpattern
@member pitch Pitch of the landpattern
@member pose Pose of the landpattern
<DOC>
public defstruct Corner-Cut-Matrix-Planner <: BGA-PadPlanner :
  doc: \<DOC>
  @title corner-cut
  @desc Integer count of the triangular region in each corner that will be inactive
  <DOC>
  corner-cut:Int

  doc: \<DOC>
  @title pad-config
  @desc Defines the features of each individual BGA pad.
  This defines the shape and parameters for the copper,
  soldermask opening, paste application, etc.
  <DOC>
  pad-config:PadConfig with:
    as-method => true
    default => DEF-PAD-CONFIG


  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; GridPlanner
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;
  doc: \<DOC>
  @title rows
  @desc Number of rows in the landpattern (should be a positive number).
  <DOC>
  rows:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title columns
  @desc Number of columns in the landpattern (should be a positive number).
  <DOC>
  columns:Int with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pitch
  @desc Pitch of the pads in landpattern (should be a positive number).
  <DOC>
  pitch:Double|Dims with:
    ensure => ensure-positive!
    as-method => true

  doc: \<DOC>
  @title pose
  @desc Pose of the landpattern pads (default is loc(0.0, 0.0))
  <DOC>
  pose:Pose with:
    as-method => true
    default => loc(0.0, 0.0)

with:
  keyword-constructor => true
  printer => true

doc: \<DOC>
@title active?
@desc Corner Cut Matrix - Query if a pad is active
@param x Corner-Cut-Matrix-Planner
@param row Row of the landpattern
@param column Column of the landpattern
<DOC>
public defmethod active? (x:Corner-Cut-Matrix-Planner, row:Int, column:Int) -> True|False:
  val r-inv = rows(x) - row - 1
  val c-inv = columns(x) - column - 1
  min(row, r-inv) + min(column, c-inv) >= corner-cut(x)
