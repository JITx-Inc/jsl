#use-added-syntax(jitx)
defpackage jsl/examples/protocols/displayport/main:
  import core
  import collections
  import jitx
  import jitx/commands


  import jsl/design/settings
  import jsl/symbols/SymbolDefn

  import jsl/protocols/displayport
  import jsl/si/helpers
  import jsl/bundles
  import jsl/si/constraints
  import jsl/si/pairs
  import jsl/si/couplers

  import jsl/examples/protocols/common/example-board
  import jsl/examples/protocols/common/example-components
  import jsl/examples/protocols/displayport/displayport-src

pcb-module displayport-example :
  pin VDD
  pin GND

  inst dut1 : jsl/examples/protocols/displayport/displayport-src/module(source = true)
  inst dut2 : jsl/examples/protocols/displayport/displayport-src/module(source = false)

  require MCU-src : displayport from dut1
  require CONN-dst : displayport from dut2

  val ti = displayport-get-trace-impedance()
  val DP-limits = DisplayPort-Constraint(gen = DP2p1, route-struct = diff(ti))

  within [src, dst] = constrain-topology(MCU-src => CONN-dst, DP-limits):
    inst cap : dp-coupler(block-cap(100.0e-9))[length(src.lane)]
    for i in 0 to length(src.lane) do:
      topo-pair(src.lane[i] => cap[i] => dst.lane[i])

    inst shunt-r : symmetric-shunt(pu-res(50.0e3))
    topo-pair(src.aux => shunt-r.A => shunt-r.B => dst.aux)
    net (shunt-r.COMMON, VDD)

    net (src.hpd, dst.hpd)

  net (VDD dut1.VDD dut2.VDD)
  net (GND dut1.GND dut2.GND)

set-current-design("displayport-example")
setup-board()
; Set the schematic sheet size
set-paper(ANSI-A)

; Set the top level module (the module to be compile into a schematic and PCB)
set-main-module(displayport-example)

; View the results
view-board()
; view-schematic()
;view-design-explorer()
; view-bom(BOM-STD)
