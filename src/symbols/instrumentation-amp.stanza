#use-added-syntax(jitx)
defpackage jsl/symbols/instrumentation-amp:
  import core
  import jitx

  import jsl/ensure
  import jsl/errors
  import jsl/symbols/framework
  import jsl/symbols/op-amps


; Increased pitch so that we can insert
;   the gain resistor pins between the
;   +/- pins
val DEF_IAMP_PITCH = 6.0
doc: \<DOC>
Default pitch for the Gain Resistor terminals
<DOC>
val DEF_IAMP_RG_PITCH = 2.0


var DEF_IAMP_PARAMS = OpAmpSymbolParams(tri-height = 5.0, tri-width = DEF_IAMP_PITCH + 3.5)
public defn get-default-iamp-symbol-params () -> OpAmpSymbolParams :
  DEF_IAMP_PARAMS

public defn set-default-iamp-symbol-params (v:OpAmpSymbolParams) -> False :
  DEF_IAMP_PARAMS = v


public val IAMP_RG_REF_DEF = Ref("Rg")
public val IAMP_VREF_REF_DEF = Ref("REF")


public defstruct InstrumentationAmpSymbol <: OpAmpSymbol :
  doc: \<DOC>
  Pos/Neg Terminal Pitch
  <DOC>
  pitch:Double with:
    ensure => ensure-positive!
    default => DEF_IAMP_PITCH
    as-method => true

  doc: \<DOC>
  Gain Resistor Terminal Pitch
  <DOC>
  rg-pitch:Double with:
    ensure => ensure-positive!
    default => DEF_IAMP_RG_PITCH

  doc: \<DOC>
  Input Base Reference or Explicit Input References.
  @see OpAmpSymbol for more Information.
  <DOC>
  in-ref:Ref|[Ref, Ref] with:
    default => OPAMP_IN_BASE_REF,
    as-method => true

  doc: \<DOC>
  Output Reference
  User can override the default for supporting multi-channel
  chips. The default expected output pin ref is `out`
  <DOC>
  out-ref:Ref with:
    default => OPAMP_OUT_REF,
    as-method => true

  doc: \<DOC>
  Gain Resistor Interface Reference
  User can override the default for supporting multi-channel
  chips. The default Rg pin ref is `Rg` and this will result
  in two pins `Rg[0]` and `Rg[1]`
  <DOC>
  rg-ref:Ref with:
    default => IAMP_RG_REF_DEF

  doc: \<DOC>
  Voltage Reference Pin RefName
  User can override the default for supporting multi-channel
  chips. The default vref pin is `REF`.
  <DOC>
  vref-ref:Ref with:
    default => IAMP_VREF_REF_DEF

  params:OpAmpSymbolParams with:
    default => get-default-iamp-symbol-params()
    as-method => true

with:
  constructor => #InstrumentationAmpSymbol

public defn InstrumentationAmpSymbol (
  --
  pitch:Double = DEF_IAMP_PITCH,
  rg-pitch:Double = DEF_IAMP_RG_PITCH
  in-ref:Ref|[Ref, Ref] = OPAMP_IN_BASE_REF,
  out-ref:Ref = OPAMP_OUT_REF,
  rg-ref:Ref = IAMP_RG_REF_DEF,
  vref-ref:Ref = IAMP_VREF_REF_DEF,
  params:OpAmpSymbolParams = get-default-iamp-symbol-params()
  ) -> InstrumentationAmpSymbol :
  if rg-pitch > pitch :
    throw $ ValueError("Invalid Pitch Configuration for Instrumentation Amp Symbol: pitch %_ > rg-pitch %_" % [pitch, rg-pitch])
  #InstrumentationAmpSymbol(pitch, rg-pitch, in-ref, out-ref, rg-ref, vref-ref, params)

public defmethod name (x:InstrumentationAmpSymbol) -> String :
  "InstrumentationAmp"

public defn add-iamp-rg-pins (x:InstrumentationAmpSymbol, node:SymbolNode) :
  val p = params(x)

  ; Add the Gain Resistor Terminals
  val rg-params = VirtualPinParams(
    direction = Left,
    pin-length = pin-length(p)
    pad-ref-size = pad-ref-size(p)
  )
  val w = rg-pitch(x) / 2.0
  val h2 = tri-height(p) / 2.0

  val rg = rg-ref(x)
  add-pin(node, rg[0], [(- h2), w], params = rg-params, name = "pin-rg-0")
  add-pin(node, rg[1], [(- h2), (- w)], params = rg-params, name = "pin-rg-1")


public defn add-iamp-ref-pin (x:InstrumentationAmpSymbol, node:SymbolNode) :
  val p = params(x)

  ; Add the Gain Resistor Terminals
  val ref-params = VirtualPinParams(
    direction = Down,
    pin-length = pin-length(p)
    pad-ref-size = pad-ref-size(p)
  )
  val w = tri-width(p)
  ; If you compute the geometry, the
  ;   y intercept of the triangle edge at x = 0.0
  ;   is (w / 4)
  val y = 0.25 * w

  add-pin(node, vref-ref(x), [0.0, (- y)], params = ref-params, name = "pin-ref")


public defmethod build-pins (x:InstrumentationAmpSymbol, node:SymbolNode) :
  add-opamp-pins(x, node)
  add-iamp-rg-pins(x, node)
  add-iamp-ref-pin(x, node)

public defn add-iamp-gain-indicator (x:InstrumentationAmpSymbol, node:SymbolNode) :
  val p = params(x)
  val lw = line-width(p)
  val rg-p = rg-pitch(x)
  val w = rg-p / 2.0
  val h2 = tri-height(p) / 2.0

  val indent = (h2 - 0.5)

  val pts = [
    [(- h2), w]
    [(- indent), w],
    [(- indent), w - 0.25],
  ]
  line(node, pts, width = lw)
  val pts2 = [
    [(- h2), (- w)]
    [(- indent), (- w)],
    [(- indent), (- (w - 0.25))],
  ]
  line(node, pts2, width = lw)
  text(node, [(- indent), 0.0] "G", font-size = 8)

public defmethod build-artwork (x:InstrumentationAmpSymbol, node:SymbolNode) :
  val child = create-child(node, name = "amp")
  build-opamp-glyphs(child, pitch(x), params(x))

  add-iamp-gain-indicator(x, node)
