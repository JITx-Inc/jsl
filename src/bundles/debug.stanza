#use-added-syntax(jitx)
defpackage jsl/bundles/debug:
  import core
  import jitx
  import jitx/commands

doc: \<DOC>
JTAG Serial Interface Bundle

Typically used for debugging/testing integrated circuits.

This bundle does not include `TRSTN` or Target Reset. Use a
separate `reset` bundle to provide that interface on a connector
or microcontroller.

@member tck Synchronous Clock Line
@member tdi Data Input Line
@member tdo Data Output Line
@member tms State Select Line
<DOC>
public pcb-bundle jtag :
  port tck
  port tdi
  port tdo
  port tms

public pcb-enum jsl/bundles/debug/SWDPins :
  SWD-SWO ; Trace Output for Serial Wire Debug

pcb-bundle swd-b (pins:Collection<SWDPins>):
  port swdio
  port swdclk
  for p in pins do:
    switch(p):
      SWD-SWO: make-port(`swo)

doc: \<DOC>
Serial Wire Debug Bundle Generator

@member swdio Synchronous Bidir Data Line
@member swdclk Synchronous Clock Line
@member swo Optional Trace Data Line
<DOC>
public defn swd (pins:SWDPins ...):
  swd-b(pins)
