doc: \<DOC>
@title MDI - Media Dependent Interface
@desc This module defines the MDI bundle and associated constraint functions
for the 1000Base-T Ethernet standard.

@snippet  Create a 1000Base-T MDI Constraint and apply it to a simple topology
```stanza
  require eth-0 : MDI-1000Base-T from ethernet-jack[0]
  require eth-1 : MDI-1000Base-T from ethernet-jack[1]
  val mdi-constraint = MDI-1000Base-T-Constraint(
    route-struct = diff-routing-struct(substrate, 100)
  )
  topo-net(eth-0, eth-1)
  constrain(mdi-constraint, eth-1, eth-0)
```

@snippet  Create a 1000Base-T MDI Constraint and apply it to a complex topology
```stanza
  val esd = diodes/ESD224DQAR/create-esd-pool(2, GND)
  val mdi-constraint = MDI-1000Base-T-Constraint(
    route-struct = diff-routing-struct(substrate, 100)
  )
  within [src, dst] = constrain-topology(eth-0 => eth-1, mdi-constraint):
    require protected-pairs:dual-pair[4] from esd
    for p in 0 to 4:
      topo-pair(src.TP[p] => protected-pairs[p].A => protected-pairs[p].B => dst.TP[p])
```
<DOC>
#use-added-syntax(jitx)
defpackage jsl/protocols/ethernet/MDI/MDI-1000Base-T:
  import core
  import jitx
  import jitx/commands

  import jsl/bundles
  import jsl/errors
  import jsl/ensure
  import jsl/si/constraints
  import jsl/si/helpers

public val NUM_PAIRS_1000Base-T = 4

doc: \<DOC>
1000Base-T MDI Bundle

@member TP : diff-pair[NUM_PAIRS_1000Base-T]. Array of 4 diff-pairs for the 4 twisted pairs
that make up the 1000Base-T medium. The pairs are zero
indexed and the mapping from index to pair identifiers is
0 => A
1 => B
2 => C
3 => D

<DOC>
public pcb-bundle MDI-1000Base-T:
  name = "1000Base-T"
  port TP : diff-pair[NUM_PAIRS_1000Base-T]

doc: \<DOC>
Construct `supports` statements for a 1000Base-T connection.

This constructs a `supports` statement for a 1000Base-T (Gigabit)
MDI interface. This includes creating the appropriate option
mappings for each differential pair.

The `b-type` Bundle is present so that the user can customize which
bundle type to construct a support statement for. This is often useful
when constructing a "Network Switch" like chip where there are auxiliary
functions like LED drivers, etc associated with that port.

@param b-type Bundle type for mapping. This bundle must define a port `TP` that
is a `PortArray` of 4 `diff-pair` Bundle type. See {@link MDI-1000Base-T} as an
example.
@param dps Tuple of [P,N] of twisted-pair sets from a component or module. This list
must be ordered in A, B, C, D pair ordering.

@snippet Example Support Declaration

```
; From 'JITx-Inc/microchip-networking'
make-1000Base-T-supports(
  MDI-1000Base-T,
  [C.TXRX1P_A, C.TXRX1M_A]
  [C.TXRX1P_B, C.TXRX1M_B]
  [C.TXRX1P_C, C.TXRX1M_C]
  [C.TXRX1P_D, C.TXRX1M_D]
)
```

<DOC>
public defn make-1000Base-T-supports (b-type:Bundle, dps:[JITXObject, JITXObject], ...):
  if length(dps) != NUM_PAIRS_1000Base-T:
    throw $ ValueError("Expected %_ diff-pair objects - Received %_" % [NUM_PAIRS_1000Base-T, length(dps)])

  ; We can swap A & B or
  ; We can swap C & D
  ; but we can mix between AB and CD - that is verboten
  ; because the auto MDI operates over AB
  val pair-maps = [
    [0, 1, 2, 3], ; Straight Through
    [1, 0, 2, 3], ; A/B swapped
    [0, 1, 3, 2], ; C/D swapped
    [1, 0, 3, 2], ; A/B & C/D swapped
  ]
  inside pcb-module:
    supports b-type:
      for pair-map in pair-maps do:
        option:
          for i in 0 to NUM_PAIRS_1000Base-T do:
            val j = pair-map[i]
            val [P, N] = [dps[j][0], dps[j][1]]
            b-type.TP[i].P => P
            b-type.TP[i].N => N

doc: \<DOC>
Construct a 1000Base-T Supports Statement

By default - this function uses {@link MDI-1000Base-T} as
the bundle type for the constructed `supports` statement.
Otherwise, it is the same as the other implementation.

@param dps Tuple of [P, N] ports that make up each of the
differential pairs that make up the A, B, C, D
twisted pairs of the MDI. Note that these arguments
are expected to be passed in the right order - otherwise,
we may setup the wrong pin assignment constraints.
This argument is expected to be length 4.
<DOC>
public defn make-1000Base-T-supports (dps:[JITXObject, JITXObject], ...):
  make-1000Base-T-supports(MDI-1000Base-T, dps = dps)

doc: \<DOC>
Construct a 1000Base-T Supports Statement

@param dps Diff-Pairs that make up the A, B, C, D
twisted pairs of the MDI. Note that these arguments
are expected to be passed in the right order - otherwise,
we may setup the wrong pin assignment constraints.
<DOC>
public defn make-1000Base-T-supports (dps:JITXObject ...):
  val dps-pairs = to-tuple $ for i in 0 to length(dps) seq:
    [dps[i].P, dps[i].N]

  make-1000Base-T-supports(dps = dps-pairs)

; Constraints
doc: \<DOC>
Retrieve the expected Intra-pair skew and diff-pair loss limits
@return Tuple of [Skew, Loss] where

*  Skew - Allowable delay difference as a `Toleranced` value in Seconds.
*  Loss - Max allowable power loss limit in dB.
<DOC>
public defn MDI-1000Base-T-get-skew-loss () -> [Toleranced, Double]:
  [0.0 +/- (1.6e-12 / 2.0), 12.0]

doc: \<DOC>
Retrieve the expected differential impedance for the differential pairs
@return Toleranced in ohms.
<DOC>
public defn MDI-1000Base-T-get-trace-impedance () -> Toleranced :
  95.0 +/- (15 %)

doc: \<DOC>
Retrieve expected Inter-pair skew

@return Allowable delay difference as a `Toleranced` value in Seconds.
<DOC>
public defn MDI-1000Base-T-get-pair-to-pair-skew () -> Toleranced :
  0.0 +/- (330.0e-12 / 2.0)


public defstruct MDI-1000Base-T-Constraint <: DiffPair-Constraint :
  doc: \<DOC>
  Intra-pair Timing Skew Limit in Seconds
  This value is typically something like `0 +/- (10.0e-12)`
  <DOC>
  skew:Toleranced  with: (as-method => true)
  doc: \<DOC>
  Max Loss Limit in dB
  <DOC>
  loss:Double with: (
    ensure => ensure-positive!,
    as-method => true
    )

  doc: \<DOC>
  Differential Routing Structure for defining the geometry of the diff-pair

  The user will define a `pcb-differential-routing-structure` and then pass that
  by ref to this field. This will define how this differential pair realizes in
  the board view.
  <DOC>
  route-struct:DifferentialRoutingStructure with: (as-method => true)

  doc: \<DOC>
  Max skew between diff-pairs (not between P/N)
  <DOC>
  inter-skew:Toleranced
with:
  constructor => #MDI-1000Base-T-Constraint

doc: \<DOC>
SI Constraint For 1000Base-T MDI

This constraint assumes the endpoints are {@link MDI-1000Base-T}
bundles. The code will construct the necessary skew
and loss constraints for this bundle according to the
pass configurations.

@param skew Intra-pair skew for the P/N of the diff-pair
@param loss Max Loss for any of the diff-pairs
@param route-struct User provided routing structure for each of the
diff-pair bundles of the MDI.
@param inter-skew Inter-pair skew between `TP` diff-pairs.
<DOC>
public defn MDI-1000Base-T-Constraint (
  --
  skew:Toleranced
  loss:Double
  route-struct:DifferentialRoutingStructure
  inter-skew:Toleranced
  ) -> MDI-1000Base-T-Constraint:
  #MDI-1000Base-T-Constraint(skew, loss, route-struct, inter-skew)

doc: \<DOC>
Construct a Constraint for 1000Base-T MDI with default specs (800fs intra-pair skew, 12dB loss, 330fs inter-pair skew)

@param route-struct User provided routing structure for each of the
diff-pair bundles of the MDI.
<DOC>
public defn MDI-1000Base-T-Constraint (
  --
  route-struct:DifferentialRoutingStructure
  ) -> MDI-1000Base-T-Constraint :
  val [s, ml] = MDI-1000Base-T-get-skew-loss()
  val int-skew = MDI-1000Base-T-get-pair-to-pair-skew()
  #MDI-1000Base-T-Constraint(s, ml, route-struct, int-skew)

public defmethod constrain (cst:MDI-1000Base-T-Constraint, src:JITXObject, dst:JITXObject) -> False :

  for i in 0 to NUM_PAIRS_1000Base-T do:
    diffpair-constrain(cst, src.TP[i], dst.TP[i])

  ; For each of the pairs - we want to institute a maximum
  ; inter-pair skew limit
  timing-window(
    inter-skew(cst)
    src.TP[0].P => dst.TP[0].P,
    src.TP => dst.TP
    )
