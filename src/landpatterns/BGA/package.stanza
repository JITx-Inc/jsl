#use-added-syntax(jitx)
defpackage jsl/landpatterns/BGA/package:
  import core
  import math
  import jitx
  import jitx/commands

  import jsl/design/settings
  import jsl/geometry/basics
  import jsl/geometry/box
  import jsl/geometry/LineRectangle
  import jsl/errors
  import jsl/ensure

  import jsl/landpatterns/packages
  import jsl/landpatterns/leads
  import jsl/landpatterns/pad-planner
  import jsl/landpatterns/numbering
  import jsl/landpatterns/VirtualLP
  import jsl/landpatterns/BGA/planner
  import jsl/landpatterns/silkscreen

doc: \<DOC>
Ball Grid Array Package
The BGA package use a 2-D grid of rows (letters) and
columns (integers). The grid is typically delineated
in row-major order.
<DOC>
public defstruct BGA <: Package :
  doc: \<DOC>
  Total number of leads for the BGA Grid
  <DOC>
  num-leads:Int with: (
    ensure => ensure-positive!,
    as-method => true
  )
  doc: \<DOC>
  Number of Rows of leads on this package
  <DOC>
  rows:Int with: (
    ensure => ensure-positive!
  )
  doc: \<DOC>
  Number of Columns of leads on this package
  <DOC>
  columns:Int with: (
    ensure => ensure-positive!
  )
  doc: \<DOC>
  BGA Ball Lead Diameter in mm
  <DOC>
  lead-diam:Double with: (
    ensure => ensure-positive!,
  )
  doc: \<DOC>
  BGA Ball Spacing in mm

  The ball spacing can either be:

  1.  Same pitch between rows and columns.
    1.  This is indicated with a `Double` value.
  2.  Different pitch between rows from between columns.
    1.  This is indicated with a `Dims` value
    2.  The width is interpretted as the column pitch
    3.  The height is interpretted as the row pitch
  <DOC>
  pitch:Double|Dims with: (
    ensure => ensure-positive!
  )
  doc: \<DOC>
  Package Body for the BGA.
  <DOC>
  package-body:PackageBody with: (as-method => true)

  doc: \<DOC>
  Pad Planner for the BGA package
  The planner determines which pads will be created in the
  grid. See {@link PadPlanner} for more info.
  <DOC>
  pad-planner:PadPlanner with: (as-method => true)

  doc: \<DOC>
  Lead Numbering Scheme for the BGA Package
  <DOC>
  lead-numbering:Numbering with: (as-method => true)

  doc: \<DOC>
  Density Level for the Generated Package
  <DOC>
  density-level:DensityLevel with: (as-method => true)
with:
  hashable => true
  printer => true
  constructor => #BGA

doc: \<DOC>
BGA Package Constructor
@param num-leads Total number of lead placements assuming
all leads are present.
@param rows Number of rows of ball leads (Y dimension)
@param columns Number of columns of ball leads (X dimension)
@param lead-diam Size of the BGA ball lead.
@param pitch Ball Spacing
@param package-body Component body physical representation.
@param pad-planner Pad Planner that decides which pads will
be populated in the resultant footprints. The default is
a {@link Full-Matrix-Planner} which populates all lead
locations with a circular pad based on its default configs.
@param lead-numbering Lead numbering generator that determines
what `Ref`s are applied to each pad. By default, this function
uses a {@link Grid-Numbering} which creates `A1`, `B7`, etc
style references using `1-based` indexing (ie, no `C0`).
<DOC>
public defn BGA (
  --
  num-leads:Int,
  rows:Int,
  columns:Int,
  lead-diam:Double,
  pitch:Double|Dims,
  package-body:PackageBody,
  pad-planner:BGA-PadPlanner = Full-Matrix-Planner(),
  lead-numbering:Numbering = Grid-Numbering(rows, columns)
  density-level:DensityLevel = DENSITY-LEVEL
) -> BGA :
  #BGA(
    num-leads, rows, columns,
    lead-diam, pitch,
    package-body,
    pad-planner,
    lead-numbering,
    density-level
  )

public defmethod name (bga:BGA) -> String:
  defn to-deci (v:Double) -> String:
    val v* = to-int( v * 100.0 )
    to-string("%_" % [v*])

  val pitchStr = match(pitch(bga)):
    (p:Double): to-deci(p)
    (pa:Dims):
      val px = to-deci $ x(pa)
      val py = to-deci $ y(pa)
      to-string("%_X%_" % [px, py])

  val ballGrid = to-string("%_X%_" % [columns(bga), rows(bga)])

  val b = package-body(bga)

  to-string("BGA%_N%_P%__%_X%_X%_" % [
    num-leads(bga),
    pitchStr,
    ballGrid,
    to-deci $ typ $ width(b),
    to-deci $ typ $ length(b),
    to-deci $ typ $ height(b),
  ])

public defmethod courtyard-excess (pkg:BGA) -> Double :
  ; From IPC 7351B Table 3-17
  switch(density-level(pkg)):
    DensityLevelA: 2.0
    DensityLevelB: 1.0
    DensityLevelC: 0.5

public defn build-vpads (
  pkg:BGA,
  vp:VirtualLP
  ):
  ; For the BGA - we size the pad according to the
  ;  ball diameter and allow the `PadConfig` object to
  ;  drive the pad sizing.
  val pad-size = Dims(lead-diam(pkg), lead-diam(pkg))
  ; Generate the grid
  val planner = pad-planner(pkg) as BGA-PadPlanner
  defn gen-pad-info () -> Seq<VirtualPad> :
    val grid = grid(planner, rows(pkg), columns(pkg), pitch(pkg))
    for r in 0 to rows(pkg) seq-cat:
      for (c in 0 to columns(pkg), pos in grid) seq? :
        val pad-id = to-pad-id(lead-numbering(pkg), r, c)
        val pad-gen? = pad-generator(planner, r, c)
        match(pad-gen?):
          (_:False): None()
          (pad-gen):
            val cls = [
              "pad",
              to-string("col-%_" % [c]),
              to-string("row-%_" % [r])
            ]
            One $ VirtualPad(pad-id, pad-gen(pad-size), pos, class = cls)

  append-all(vp, gen-pad-info())

public defmethod build-pads (
  pkg:BGA,
  vp:VirtualLP,
  ) -> False :
  build-vpads(pkg, vp)

public defn compute-outline (
  vp:VirtualLP,
  body:PackageBody,
  density-level:DensityLevel
  --
  line-width:Double = default-silk-width(),
  mask-clearance:Double = default-mask-clearance(),
  ) -> Shape:

  val pkg-outline = bounds $ envelope(body, density-level = density-level)
  val lp-pads = get-pads(vp)
  val pad-outline = bounds(lp-pads, layer-spec = SolderMask(Top))
  val overall = union(pkg-outline, pad-outline)
  LineRectangle(overall, line-width = line-width)

public defn build-outline (
  vp:VirtualLP,
  body:PackageBody,
  density-level:DensityLevel
  ) -> False:
  val outline = compute-outline(vp, body, density-level)
  add-artwork(vp, Silkscreen("outline", Top), outline, class = "outline")

defn get-outline (vp:VirtualLP) -> Shape:
  val outlines = to-tuple $ seq{as-VirtualArtwork, _} $ find-by-class(vp, "outline")
  if length(outlines) != 1:
    throw $ ValueError("Expected only one element in class 'outline': %," % [outlines])

  shape $ outlines[0]

public defn build-triangle-marker (
  vp:VirtualLP
  --
  pin-1-id:Int|Ref = 1,
  side:Side = Top
  ):
  val o-sh = get-outline(vp)

  val pin-1-pad = get-pad-by-ref!(vp, pin-1-id)

  ; The Outline is typically a line rectangle which creates a
  ;  single line. We use this to determine the line width
  ;  so that we can place the marker in the mid-line
  val line-width = match(o-sh):
    (x:Line): width(x)
    ; TODO - this probably should be a setting
    (_): 0.1

  val o-box = bounds $ o-sh

  val pin-1-pos = center $ pose(pin-1-pad)

  val marker = compute-triangle-marker(pin-1-pos, o-box, line-width)

  add-artwork(
    vp, Silkscreen("pin-1-marker", side),
    marker,
    name = "pin-1-triangle",
    class = "pin-1-marker"
    )

public defn build-pin-1-marker (
  vp:VirtualLP
  --
  pin-1-id:Int|Ref = 1,
  side:Side = Top
  line-width:Double = default-silk-width(),
  mask-clearance:Double = default-mask-clearance(),
  ):
  val o-sh = get-outline(vp)
  val pin-1-pos = center $ pose $ get-pad-by-ref!(vp, pin-1-id)

  val o-box = bounds $ o-sh
  val closest = compute-closest-corner(pin-1-pos, o-box)

  val corner = compute-corner(closest)
  val rot = corner-to-rotation(corner)

  val x-offset = (1.5 * line-width) + mask-clearance
  val y-offset = line-width
  val marker-shape = Circle((- x-offset), (- y-offset), line-width)
  val marker-pose = loc(closest, rot)
  val marker = marker-pose * marker-shape

  add-artwork(
    vp, Silkscreen("pin-1-marker", side),
    marker,
    name = "pin-1-dot",
    class = "pin-1-marker"
    )

public defmethod build-silkscreen (
  pkg:BGA,
  vp:VirtualLP,
  ):
  val pin-1-id = IndexRef(Ref("A"), 1)
  build-outline(vp, package-body(pkg), density-level(pkg))
  build-pin-1-marker(vp, pin-1-id = pin-1-id)
  build-triangle-marker(vp, pin-1-id = pin-1-id)
