#use-added-syntax(jitx)
defpackage jsl/examples/protocols/pcie/main:
  import core
  import collections
  import jitx
  import jitx/commands


  import jsl

  import jsl/examples/protocols/common/example-board
  import jsl/examples/protocols/common/example-components
  import jsl/examples/protocols/pcie/pcie-src


pcb-module pcie-example :

  inst dut1 : jsl/examples/protocols/pcie/pcie-src/module
  inst dut2 : jsl/examples/protocols/pcie/pcie-conn/module
  inst dut3 : jsl/examples/protocols/pcie/pcie-conn/module

  val version = PCIE-V4
  val trace-imped = pcie-get-trace-impedance(version)
  val cst = PCIe-Constraint(version, diff(trace-imped))

  val b-cap = block-cap(220.0e-9)

  ; Construct a typical passive connector setup
  ;  for a 2 lane configuration. This means a
  ;  straight through `tx => tx` and `rx => rx`
  ;  configuration.
  ;
  ;  The two connectors share the one wake pin from the IC source module

  require src-ep : pcie-std(2) from dut1
  require dst-ep : pcie-std(2)[2] from dut2

  for i in 0 to 2 do:
    within [src, dst] = constrain-topology(src-ep, dst-ep[i], cst):
      ; Here we construct the circuit topology for the link
      ;   Note that we don't need to worry about any of the constraint
      ;   application, as that is handled by the `PCIe-Constraint` type.
      ;   You can add other components in the topology as you wish - below
      ;   is a typical basic implementation.
      for i in indices(src.data.lane) do:
        inst tx-coupler : dp-coupler(b-cap)
        topo-pair(src.data.lane[i].TX => tx-coupler => dst.data.lane[i].TX)
        ; No Blocking Caps on the Receive side.
        topo-net(src.data.lane[i].RX => dst.data.lane[i].RX)

      topo-net(src.data.refclk => dst.data.refclk)
      ; The control signals do not demand a topology so
      ;  we just use a straight net connection.
      net (src.control, dst.control)




set-current-design("pcie-example-2")
setup-board()
; Set the schematic sheet size
set-paper(ANSI-A)

; Set the top level module (the module to be compile into a schematic and PCB)
set-main-module(pcie-example)

; View the results
view-board()
view-schematic()
view-design-explorer()
; view-bom(BOM-STD)
