#use-added-syntax(jitx)
defpackage jsl/landpatterns/courtyard:
  import core
  import jitx
  import jitx/commands

  import jsl/design/settings
  import jsl/landpatterns/helpers
  import jsl/geometry/basics
  import jsl/geometry/box
  import jsl/landpatterns/VirtualLP
  import jsl/landpatterns/packages with:
    only => (PackageBody, envelope)
  ; TODO - This ideally moves to jitx
  import jsl/geometry/LineRectangle

public val DEF_COURTYARD_WIDTH = 0.1

public defn build-courtyard-origin (
  vp:VirtualLP,
  --
  pose:Pose = loc(0.0, 0.0)
  side:Side = Top
  ) -> False:

  val shape = PlusSymbol(line-width = DEF_COURTYARD_WIDTH, pose = pose)
  add-artwork(vp, Courtyard(side), shape, class = "courtyard")

doc: \<DOC>
Generator for Courtyard Origin Marker

This function generates a `plus` sign marker
at the origin of a component landpattern.

@param pose Override the origin location by applying a
`pose`. This can both rotate and translate the marker position.
@param side Indicates which side to place the courtyard on. Default
per convention is the top.
<DOC>
public defn make-courtyard-origin (
  --
  pose:Pose = loc(0.0, 0.0)
  side:Side = Top
  ):
  inside pcb-landpattern:
    layer(Courtyard(side)) = PlusSymbol(line-width = DEF_COURTYARD_WIDTH, pose = pose)


doc: \<DOC>
Generate the Bounding Dims of a sequence of pads
@param lp-pads the pads that we want to create a bounding
rectangle of.
@param layer-spec Optional layer specifier that will compute
  the bounds based on the geometry in a particular layer.
  If this value is false - we compute the boundary based on the
  copper pad geometry only.
@param expand-by Amount to expand the created dimension by.
<DOC>
public defn bounds (
  lp-pads:Seqable<LandPatternPad|VirtualPad>
  --
  layer-spec:LayerSpecifier|False = false
  expand-by:Double = 0.0
  ) -> Box :

  val unionBox = reduce{union, _} $ for lp-pad in lp-pads seq:
    val pd = pad(lp-pad)
    val l = pose(lp-pad)
    match(layer-spec):
      (_:False):
        bounds(l * pad-shape(pd))
      (ls:LayerSpecifier):
        val shapes = layer(pd, ls)
        bounds(l * Union(shapes))

  fatten(expand-by, unionBox)

public defn compute-courtyard-boundary (
  lp-pads:Seqable<LandPatternPad|VirtualPad>,
  pkg-body:PackageBody,
  density-level:DensityLevel,
  excess:Double,
  side:Side,
  pos:Pose,
  ):
  ; @NOTE - Here I'm using the soldermask openings for the pads
  ;   as the controlling boundary/outline. By doing this -
  ;   I can enforce at least a sliver of soldermask between
  ;   components.
  val pad-outline = bounds(lp-pads, layer-spec = SolderMask(side))
  val pkg-outline = bounds(envelope(pkg-body, density-level = density-level))
  val outline = union(pad-outline, pkg-outline)
  val outline* = fatten(excess, outline)
  val rect = to-Rectangle(outline*)
  pos * LineRectangle(width(rect), height(rect), line-width = DEF_COURTYARD_WIDTH, pose = pose(rect))


doc: \<DOC>
Generator for Courtyard Boundary

The courtyard boundary creates the typical minimum spacing
expected around a component. This spacing is for mechanical assembly
and thermal reflow reasons.

This function uses the copper pads and the package body to define the
envelope of the component. This envelope is then expanded with the `excess`
amount on all sides. We then compute the smallest rectangle that
encapsulates all of these expanded features. This rectangle forms
the courtyard boundary.

@param pkg-body Package Body of the component
@param excess Additional space to pad the boundary of the
pads for this component when making the courtyard boundary.
The default value is 0.5mm
@param side Which side of the board to draw the courtyard on. By
default we use the `Top` side as per convention.
@param pose Pose offset for this group of features. Default is no transform.
@param density-level
<DOC>
public defn make-courtyard-boundary (
  pkg-body:PackageBody
  density-level:DensityLevel
  --
  excess:Double = 0.5
  side:Side = Top
  pos:Pose = loc(0.0, 0.0),
  ):
  inside pcb-landpattern:
    val outline = compute-courtyard-boundary(pads(self), pkg-body, density-level, excess, side, pos)
    layer(Courtyard(side)) = outline

doc: \<DOC>
Create a courtyard boundary in a Virtual Landpattern

@param vp Virtual Landpattern Container
@param pkg-body Package Body for the Component
@param density-level
<DOC>
public defn build-courtyard-boundary (
  vp:VirtualLP,
  pkg-body:PackageBody
  density-level:DensityLevel
  --
  excess:Double = 0.5
  pos:Pose = loc(0.0, 0.0)
  side:Side = Top,
  ) -> False:
  val outline = compute-courtyard-boundary(get-pads(vp), pkg-body, density-level, excess, side, pos)
  add-artwork(vp, Courtyard(side), outline, class = "courtyard")

doc: \<DOC>
Retrieve the Dimensions of the Courtyard Boundary
<DOC>
public defn get-courtyard-boundary (
  vp:VirtualLP
  --
  side:Side = Top
  ) -> Box|False:
  val content = get-layer(vp, Courtyard(side))
  if length(content) == 0: false
  else:
    bounds $ Union(content)
