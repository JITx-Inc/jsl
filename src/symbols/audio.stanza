#use-added-syntax(jitx)
defpackage jsl/symbols/audio:
  import core
  import math
  import jitx

  import maybe-utils

  import jsl/ensure
  import jsl/geometry/box
  import jsl/symbols/framework

val DEF_AUDIO_LINE_WIDTH = 0.05
val DEF_AUDIO_PIN_LEN = 2.0
val DEF_AUDIO_PAD_REF_SIZE = 0.65

public defstruct AudioSymbolParams <: SymbolParams :
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width
    default => DEF_AUDIO_LINE_WIDTH
  pin-length:Double with:
    ensure => ensure-positive!
    updater => sub-pin-length
    default => DEF_AUDIO_PIN_LEN
  pad-ref-size:Double with:
    ensure => ensure-positive!
    updater => sub-pad-ref-size
    default => DEF_AUDIO_PAD_REF_SIZE
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()
with:
  keyword-constructor => true
  printer => true
  equalable => true


var DEF_AUDIO_PARAMS = AudioSymbolParams()
public defn get-default-audio-symbol-params () -> AudioSymbolParams :
  DEF_AUDIO_PARAMS

public defn set-default-audio-symbol-params (v:AudioSymbolParams) -> False :
  DEF_AUDIO_PARAMS = v


val DEF_AUDIO_PITCH = TWO_PIN_DEF_PITCH


public defstruct SpeakerSymbol <: SymbolDefn :
  pitch:Double with:
    default => DEF_AUDIO_PITCH
    ensure => ensure-positive!
  params:Maybe<AudioSymbolParams> with:
    default => None()
with:
  keyword-constructor => true
  printer => true

public defmethod name (x:SpeakerSymbol) -> String :
  "Speaker"

defmethod get-params (x:SpeakerSymbol) -> AudioSymbolParams :
  value-or-else(params(x) as Maybe<AudioSymbolParams>, get-default-audio-symbol-params)

public defmethod build-pins (x:SpeakerSymbol, node:SymbolNode) :
  val p = get-params(x) as AudioSymbolParams

  val input-params = VirtualPinParams(
    direction = Left,
    pin-length = pin-length(p),
    pad-ref-size = pad-ref-size(p)
  )

  val p2 = pitch(x) / 2.0

  add-pin(node, PINREF[1], Point(0.0, p2), params = input-params, name = to-string("pin-1") )
  add-pin(node, PINREF[2], Point(0.0, (- p2)), params = input-params, name = to-string("pin-2") )

public defn build-speaker-glyphs (
  node:SymbolNode,
  pitch:Double,
  p:AudioSymbolParams
  ) :
  val lw = line-width(p)
  val h = 2.0 * pitch
  val w = h / 2.0

  val h2 = h / 2.0
  val box = [
    Point(0.0, h2),
    Point(w, h2),
    Point(w, (- h2)),
    Point(0.0, (- h2))
    Point(0.0, h2)
  ]
  line(node, box, width = lw, name = "box" )

  val cone = [
    Point(w, h2),
    Point(2.0 * w, 2.0 * h2),
    Point(2.0 * w, -2.0 * h2),
    Point(w, (- h2)),
  ]
  line(node, cone, width = lw, name = "cone")

public defmethod build-artwork (x:SpeakerSymbol, node:SymbolNode) :
  val p = get-params(x) as AudioSymbolParams
  build-speaker-glyphs(node, pitch(x), p)


public defmethod build-params (x:SpeakerSymbol, sn:SymbolNode) :
  val b = glyph-bounds(sn)
  val p = get-label-params $ get-params(x)
  build-labels(
    p,
    sn,
    LabelPosition([0.0, down(b)], SW)
    LabelPosition([0.0, down(b) - 0.5], NW)
    )


public defstruct BuzzerSymbol <: SymbolDefn :
  pitch:Double with:
    default => DEF_AUDIO_PITCH
    ensure => ensure-positive!
  params:Maybe<AudioSymbolParams> with:
    default => None()
with:
  keyword-constructor => true
  printer => true

defmethod get-params (x:BuzzerSymbol) -> AudioSymbolParams :
  match(params(x)):
    (_:None): get-default-audio-symbol-params()
    (v:One<AudioSymbolParams>): value(v)

public defmethod name (x:BuzzerSymbol) -> String :
  "Buzzer"

public defmethod build-pins (x:BuzzerSymbol, node:SymbolNode) :
  val p = get-params(x) as AudioSymbolParams

  val input-params = VirtualPinParams(
    direction = Left,
    pin-length = pin-length(p),
    pad-ref-size = pad-ref-size(p)
  )

  val p2 = pitch(x) / 2.0

  add-pin(node, PINREF[1], Point(0.0, p2), params = input-params, name = to-string("pin-1") )
  add-pin(node, PINREF[2], Point(0.0, (- p2)), params = input-params, name = to-string("pin-2") )

public defn build-buzzer-glyphs (
  node:SymbolNode,
  pitch:Double,
  p:AudioSymbolParams
  ):

  val lw = line-width(p)

  ; Compute the shape of the circle by
  ; using the two pin locations as two of
  ; the corners of a equilateral triangle that
  ; are on the circle's edge. The third corner is
  ; at the center of the circle.

  val R = 2.0 * (pitch / 2.0)
  val x-offset = sqrt(3.0) * (pitch / 2.0)

  val semicirc = Arc(
    Point(x-offset, 0.0),
    R,
    90.0,
    180.0
  )
  polyline(node, [semicirc, Point(x-offset, R)], width = lw, name = "circle")


public defmethod build-artwork (x:BuzzerSymbol, node:SymbolNode) :
  val p = get-params(x) as AudioSymbolParams
  build-buzzer-glyphs(node, pitch(x), p)

public defmethod build-params (x:BuzzerSymbol, sn:SymbolNode) :
  val b = glyph-bounds(sn)
  val p = get-label-params $ get-params(x)
  build-labels(
    p,
    sn,
    LabelPosition([0.0, down(b)], NW)
    LabelPosition([0.0, down(b) - 1.5], NW)
    )



public defstruct MicrophoneSymbol <: SymbolDefn :
  pitch:Double with:
    default => DEF_AUDIO_PITCH
    ensure => ensure-positive!
  params:Maybe<AudioSymbolParams> with:
    default => None()
with:
  keyword-constructor => true
  printer => true

defmethod get-params (x:MicrophoneSymbol) -> AudioSymbolParams :
  match(params(x)):
    (_:None): get-default-audio-symbol-params()
    (v:One<AudioSymbolParams>): value(v)

public defmethod name (x:MicrophoneSymbol) -> String :
  "Microphone"

public defmethod build-pins (x:MicrophoneSymbol, node:SymbolNode) :
  val p = get-params(x) as AudioSymbolParams

  val input-params = VirtualPinParams(
    direction = Left,
    pin-length = pin-length(p),
    pad-ref-size = pad-ref-size(p)
  )

  val p2 = pitch(x) / 2.0

  add-pin(node, PINREF[1], Point(0.0, p2), params = input-params, name = to-string("pin-1") )
  add-pin(node, PINREF[2], Point(0.0, (- p2)), params = input-params, name = to-string("pin-2") )

public defn build-microphone-glyphs (
  node:SymbolNode,
  pitch:Double,
  p:AudioSymbolParams
  ):

  ; Compute the shape of the circle by
  ; using the two pin locations as two of
  ; the corners of a equilateral triangle that
  ; are on the circle's edge. The third corner is
  ; at the center of the circle.

  val R = 2.0 * (pitch / 2.0)
  val x-offset = sqrt(3.0) * (pitch / 2.0)

  val circ = Arc(
    Point(x-offset, 0.0),
    R,
    0.0,
    360.0
  )
  val lw = line-width(p)
  polyline(node, [circ], width = lw, name = "circle")
  val line-x = x-offset + R

  line(node, [Point(line-x, R), Point(line-x, (- R))], width = 3.0 * lw, name = "diagram")

public defmethod build-artwork (x:MicrophoneSymbol, node:SymbolNode) :
  val p = get-params(x) as AudioSymbolParams
  build-microphone-glyphs(node, pitch(x), p)

public defmethod build-params (x:MicrophoneSymbol, sn:SymbolNode) :
  val b = glyph-bounds(sn)
  val p = get-label-params $ get-params(x)

  build-labels(
    p,
    sn,
    LabelPosition([0.0, down(b)], NW)
    LabelPosition([0.0, down(b) - 1.5], NW)
    )
