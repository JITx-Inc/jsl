#use-added-syntax(jitx,tests)
defpackage jsl/tests/layerstack:
  import core
  import jitx
  import jitx/commands

  import jsl/layerstack
  import jsl/tests/utils
  import jsl/errors

deftest(layerstack) test-basic-symmetric:
  val stack = LayerStack(name = "Carl's Crazy Stack")

  val copper-1oz = Copper("cu", 1.0)
  val core-FR4 = FR4("core", 1.0)
  val prepreg-FR4 = FR4("prepreg", 0.5)

  add-symmetric(copper-1oz, core-FR4,
    add-symmetric(copper-1oz, prepreg-FR4,
      add-symmetric(copper-1oz, core-FR4, stack)
    )
  )

  validate!(stack)

  val [prepreg-2, cu-2, core-2] = get-conductor(stack, 2)
  #EXPECT(prepreg-2 is-not None)
  #EXPECT(core-2 is-not None)

  val [prepreg-0, cu-0, core-0] = get-conductor(stack, 0)
  #EXPECT(prepreg-0 is None)
  #EXPECT(core-0 is-not None)

  val [prepreg-5, cu-5, core-5] = get-conductor(stack, 5)
  #EXPECT(prepreg-5 is-not None)
  #EXPECT(core-5 is None)

  ; For debug
  ; val pcb-stack = create-pcb-stackup(stack)
  ; print-def(pcb-stack)

deftest(layerstack) test-validate:

  val empty = LayerStack(name = "Empty Stack")
  expect-throw({validate!(empty)})

  val stack = LayerStack(name = "TwoCoppers")

  val copper-1oz = Copper("cu", 1.0)
  val core-FR4 = FR4("core", 1.0)

  add-symmetric(copper-1oz, core-FR4, stack)
  add-top(copper-1oz, stack)

  expect-throw({validate!(stack)})

  ; Two Dielectrics in a row

  val stack2 = LayerStack(name = "TwoDielectrics")

  add-symmetric(copper-1oz, core-FR4, stack2)
  add-bottom(core-FR4, stack2)
  add-bottom(core-FR4, stack2)

  expect-throw({validate!(stack2)})

deftest(layerstack) test-compute-thickness:

  val testvecs = [
    [0.5,  0.0174]
    [1.0,  0.0348]
    [2.0,  0.0696]
    [3.0,  0.1044]
    [4.0,  0.1392]
  ]
  for testvec in testvecs do:
    val [weight, exp-thick] = testvec
    val thick = compute-thickness(CopperMaterial, weight)
    #EXPECT(almost-equal?(thick, exp-thick, 0.001))

deftest(layerstack) test-get-conductor:
  val stack = LayerStack(name = "6-layer")

  val w = 1.0
  val copper-1oz = Copper("cu", w)
  val core-FR4 = FR4("core", 1.0)
  val prepreg-FR4 = FR4("prepreg", 0.5)

  add-symmetric(copper-1oz, core-FR4,
    add-symmetric(copper-1oz, prepreg-FR4,
      add-symmetric(copper-1oz, core-FR4, stack)
    )
  )

  validate!(stack)

  #EXPECT(get-conductor-count(stack) == 6)

  val [prepreg-2?, cu-2, core-2?] = get-conductor(stack, 2)
  #EXPECT(prepreg-2? is-not None)
  #EXPECT(core-2? is-not None)

  val prepreg-2 = value!(prepreg-2?)
  val core-2 = value!(core-2?)

  #EXPECT(name(prepreg-2) is-not None)
  #EXPECT(value!(name(prepreg-2)) == "prepreg")
  #EXPECT(description(prepreg-2) is None)
  #EXPECT(thickness(prepreg-2) == 0.5)

  #EXPECT(name(core-2) is-not None)
  #EXPECT(value!(name(core-2)) == "core")
  #EXPECT(description(core-2) is None)
  #EXPECT(thickness(core-2) == 1.0)

  #EXPECT(name(cu-2) is-not None)
  #EXPECT(value!(name(cu-2)) == "cu")
  #EXPECT(description(cu-2) is None)

  val exp-thick = 0.0348 ; mm
  #EXPECT(almost-equal?(thickness(cu-2), exp-thick, 0.001))


  ; Check the `get-copper` under failure cases:

  expect-throw({get-conductor(stack, -1)})
  expect-throw({get-conductor(stack, 10)})


deftest(layerstack) test-with-soldermask:

  val stack = LayerStack(name = "2-layer")

  val w = 2.0
  val copper-1oz = Copper("cu", w)
  val core-FR4 = FR4("core", 1.0)

  add-symmetric(copper-1oz, core-FR4, stack)
  add-soldermask(Soldermask(0.025), stack)

  validate!(stack)

  #EXPECT(get-conductor-count(stack) == 2)

  val [sm?, cu-0, core-0?] = get-conductor(stack, 0)
  #EXPECT(sm? is-not None)
  #EXPECT(core-0? is-not None)

  val sm = value!(sm?)
  val core-0 = value!(core-0?)

  #EXPECT(almost-equal?(thickness(sm), 0.025))

  val [core-1?, cu-1, sm2?] = get-conductor(stack, 1)
  #EXPECT(sm2? is-not None)
  #EXPECT(core-1? is-not None)

;============================================================================
;<note>
My Crazy Stack - each layer is different for testing
 0 - copper-3oz
 1 - core-FR4-2
 2 - copper-2oz
 3 - prepreg-FR4
 4 - copper-1oz
 5 - core-FR4-1
 6 - copper-1oz
 7 - prepreg-FR4
 8 - copper-2oz
 9 - core-FR4-2
 10 - copper-3oz
;<note>
val test-stack = LayerStack(name = "My Crazy Stack")
val copper-1oz = Copper("cu1", 1.0)
val copper-2oz = Copper("cu2", 2.0)
val copper-3oz = Copper("cu3", 3.0)
val core-FR4-1 = FR4("core1", 1.0)
val core-FR4-2 = FR4("core2", 2.0)
val prepreg-FR4 = FR4("prepreg", 0.5)

add-symmetric(copper-3oz, core-FR4-2,
  add-symmetric(copper-2oz, prepreg-FR4,
    add-symmetric(copper-1oz, core-FR4-1, test-stack)
  )
)
validate!(test-stack)

defmethod write (o:OutputStream, s:LayerStack) :
  println(o, "LayerStack: %_" % [value?(name(s))])
  println(o, "  Layers: %_ copper layers %_ total layers" % [get-conductor-count(s) length(layers(s))])
  for [idx, l] in enumerate(layers(s)) do:
    println(o, "    %_ : %_ | %_ | %_" %
      [idx, value?(name(l)) get-type(material(l)) thickness(l)])
print("%~" % [test-stack])

doc:\<DOC>
  Usage: for [idx, line] in enumerate(lines) do: 
           println(“Line %: %_” % [idx, line])
<DOC>
defn enumerate<?T> (s:Seqable<?T>) -> Seq<[Int, T]> :
  var i = 0
  for x in s seq :
    next where:
      val next = [i, x]
      i = i + 1

defmethod equal? (la:LayerSpec, lb:LayerSpec) :
  name(la) == name(lb) and
  material(la) == material(lb) and
  thickness(la) == thickness(lb)

defmethod equal? (ma:LayerMaterial, mb:LayerMaterial) :
  name(ma) == name(mb) and
  type(ma) == type(mb) and
  material-def(ma) == material-def(mb)

; Test: test-stack[0]
deftest(layerstack) test-get :
  #EXPECT(test-stack[0] == copper-3oz)
  #EXPECT(test-stack[1] == core-FR4-2)
  #EXPECT(test-stack[2] == copper-2oz)
  #EXPECT(test-stack[3] == prepreg-FR4)

; Test: conductors(test-stack)[0]
deftest(layerstack) test-conductors :
  #EXPECT(conductors(test-stack)[0] == copper-3oz)
  #EXPECT(conductors(test-stack)[1] == copper-2oz)
  #EXPECT(conductors(test-stack)[2] == copper-1oz)
  #EXPECT(conductors(test-stack)[3] == copper-1oz)
  #EXPECT(conductors(test-stack)[4] == copper-2oz)
  #EXPECT(conductors(test-stack)[5] == copper-3oz)

; Test: set-name(test-stack[0] One("cu3-new"))
deftest(layerstack) test-set-name :
  val copper-3oz = Copper("cu3", 3.0)
  #EXPECT(test-stack[0] == copper-3oz)
  val copper-3oz-new = Copper("cu3-new", 3.0)
  set-name(test-stack[0] One("cu3-new"))
  ;Verify that the layer has the new name
  #EXPECT(test-stack[0] == copper-3oz-new)

; Test: test-stack[0] = new-stack
deftest(layerstack) test-set :
  #EXPECT(test-stack[0] == copper-3oz)
  test-stack[0] = copper-1oz
  #EXPECT(test-stack[0] == copper-1oz)

  #EXPECT(test-stack[1] == core-FR4-2)
  test-stack[1] = core-FR4-1
  #EXPECT(test-stack[1] == core-FR4-1)


deftest(layerstack) test-thinkness-argument :
  val copper-1oz-with-weight = Copper("cu", 1.0)
  val thickness = thickness(copper-1oz-with-weight)
  val copper-1oz-with-thickness = Copper("cu", thickness, is-thickness? = true)
  #EXPECT(copper-1oz-with-weight == copper-1oz-with-thickness)
  
  val aluminum-1oz-with-weight = Copper("cu", 1.0, AluminumMaterial)
  val aluminum-thickness = /thickness(aluminum-1oz-with-weight)
  val aluminum-1oz-with-thickness = Copper("cu", aluminum-thickness, AluminumMaterial, is-thickness? = true)
  #EXPECT(aluminum-1oz-with-weight == aluminum-1oz-with-thickness)
