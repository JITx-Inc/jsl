#use-added-syntax(jitx)
defpackage jsl/symbols/diodes:
  import core
  import jitx

  import jsl/errors
  import jsl/ensure

  import jsl/design/Classable
  import jsl/symbols/framework


val DEF_BODY_WIDTH = 0.6
val DEF_LINE_WIDTH = 0.05
val DEF_FILLED? = false

doc: \<DOC>
Base Diode Symbol Parameters

These parameters define the features of a generic
diode symbol without accoutrements.

    |
  =====
   / \
  /   \
  -----
    |

The `pitch` of the two-pin component defines the overall length.


<DOC>
public defstruct DiodeSymbolParams <: SymbolParams :
  body-dims:Dims|Double with:
    ensure => ensure-positive!,
    updater => sub-body-dims
    default => DEF_BODY_WIDTH
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width
    default => DEF_LINE_WIDTH
  filled?:True|False with:
    updater => sub-filled?
    default => DEF_FILLED?
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()
with:
  keyword-constructor => true
  equalable => true
  printer => true


public defn to-tuple (p:DiodeSymbolParams) -> [Double|Dims, Double, True|False]:
  [body-dims(p), line-width(p), filled?(p)]

public defn compute-body-start (p:DiodeSymbolParams) -> Double :
  match(body-dims(p)):
    (w:Double):
      ; We use a square aspect ratio for the symbol
      ;  based on the `body-width`
      w / 2.0
    (d:Dims):
      y(d) / 2.0

public defn body-width (p:DiodeSymbolParams) -> Double :
  match(body-dims(p)):
    (w:Double): w
    (d:Dims): x(d)

public defn body-height (p:DiodeSymbolParams) -> Double :
  match(body-dims(p)):
    (h:Double): h
    (d:Dims): y(d)

var DEF_DIODE_PARAMS = DiodeSymbolParams()
public defn get-default-diode-symbol-params () -> DiodeSymbolParams :
  DEF_DIODE_PARAMS

public defn set-default-diode-symbol-params (v:DiodeSymbolParams) -> False :
  DEF_DIODE_PARAMS = v


public defn build-diode-glyphs (
  node:SymbolNode,
  pitch:Double,
  params:DiodeSymbolParams
  ) :
  val [body-dims, line-width, filled?] = to-tuple(params)

  val bw2 = body-width(params) / 2.0
  val p2 = pitch / 2.0

  val h = compute-body-start(params)

  if h > p2 :
    throw $ ValueError("Invalid Diode Symbol Dimension: Body is too Large: body=%_ pitch=%_" % [body-dims, pitch])

  line(node, [Point(0.0, (- p2)), Point(0.0, (- h))], width = line-width, name = "front-porch")
  line(node, [Point( (- bw2), (- h)), Point(bw2, (- h))], width = line-width, name = "T-bar")

  val tri-pts = [Point(0.0, (- h)), Point(bw2, h), Point((- bw2), h), Point(0.0, (- h))]
  if filled?:
    polygon(node, tri-pts, name = "triangle")
  else:
    line(node, tri-pts, width = line-width, name = "triangle")

  line(node, [Point(0.0, h), Point(0.0, p2)], width = line-width, name = "back-porch")


public defstruct DiodeSymbol <: TwoPinSymbol :
  doc: \<DOC>
  Pitch between the Pins of a Diode Symbol
  This value is in symbol grid units (not mm)
  <DOC>
  pitch:Double with:
    as-method => true
    default => TWO_PIN_DEF_PITCH
  polarized?:True|False with:
    as-method => true
    default => true
  params:Maybe<DiodeSymbolParams> with:
    as-method => true
    default => None()
with:
  printer => true
  keyword-constructor => true

public defmethod name (x:DiodeSymbol) -> String :
  "Diode"

defmethod get-params (x:DiodeSymbol) -> DiodeSymbolParams :
  match(params(x)):
    (_:None): get-default-diode-symbol-params()
    (given:One<DiodeSymbolParams>): value(given)

public defmethod build-artwork (
  x:DiodeSymbol, sn:SymbolNode
  ):
  val p = get-params(x) as DiodeSymbolParams
  build-diode-glyphs(sn, pitch(x), p)

var CURR-DIODE-SYMBOL:TwoPinSymbol = DiodeSymbol()
public defn get-default-diode-symbol () : CURR-DIODE-SYMBOL
public defn set-default-diode-symbol (symb:TwoPinSymbol) :
  CURR-DIODE-SYMBOL = symb


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Schottky Diode
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val DEF_WING_SIZE = 0.1


public defstruct SchottkyDiodeSymbolParams <: DiodeSymbolParams :
  wing-size:Double with:
    ensure => ensure-positive!
    updater => sub-wing-size
    default => DEF_WING_SIZE
  body-dims:Dims|Double with:
    ensure => ensure-positive!
    updater => sub-body-dims
    as-method => true
    default => DEF_BODY_WIDTH
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width
    as-method => true
    default => DEF_LINE_WIDTH
  filled?:True|False with:
    updater => sub-filled?
    as-method => true
    default => DEF_FILLED?
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()
with:
  keyword-constructor => true
  equalable => true
  printer => true

public defn sub-base-params (p:SchottkyDiodeSymbolParams, b:DiodeSymbolParams) -> SchottkyDiodeSymbolParams :
  p $> sub-body-dims{_, body-dims(b)}
    $> sub-line-width{_, line-width(b)}
    $> sub-filled?{_, filled?(b)}
    $> {_ as SchottkyDiodeSymbolParams}

var CUSTOM_SCHOTTKY_PARAMS:True|False = false
var DEF_SCHOTTKY_DIODE_PARAMS = SchottkyDiodeSymbolParams()
public defn get-default-schottky-diode-symbol-params () -> SchottkyDiodeSymbolParams :
  if CUSTOM_SCHOTTKY_PARAMS:
    DEF_SCHOTTKY_DIODE_PARAMS
  else:
    sub-base-params(DEF_SCHOTTKY_DIODE_PARAMS, get-default-diode-symbol-params())


doc: \<DOC>
Determine if the new params for the schottky diode symbol modifies the base parameters
<DOC>
defn check-non-base-defaults (p:SchottkyDiodeSymbolParams) -> True|False :
  val b = get-default-diode-symbol-params()
  (p as DiodeSymbolParams) != b

public defn set-default-schottky-diode-symbol-params (v:SchottkyDiodeSymbolParams) -> False :
  CUSTOM_SCHOTTKY_PARAMS = check-non-base-defaults(v)
  DEF_SCHOTTKY_DIODE_PARAMS = v

public defn build-schottky-diode-glyphs (
  node:SymbolNode,
  pitch:Double,
  params:SchottkyDiodeSymbolParams
  ) :
  build-diode-glyphs(node, pitch, params as DiodeSymbolParams)

  val [body-dims, line-width, filled?] = to-tuple(params)
  val wing-size = wing-size(params)

  val bw2 = body-width(params) / 2.0
  val h = compute-body-start(params)

  val wing-shape = Line(line-width, [Point(0.0, 0.0), Point(0.0, (- wing-size)), Point(wing-size, (- wing-size))])

  val L-wing = loc((- bw2), (- h)) * wing-shape
  add-glyph(node, L-wing, name? = One("L-wing"))
  val R-wing = loc(bw2, (- h), 180.0) * wing-shape
  add-glyph(node, R-wing, name? = One("R-wing"))


public defstruct SchottkyDiodeSymbol <: TwoPinSymbol :
  doc: \<DOC>
  Pitch between the Pins of a Diode Symbol
  This value is in symbol grid units (not mm)
  <DOC>
  pitch:Double with:
    as-method => true
    default => TWO_PIN_DEF_PITCH
  polarized?:True|False with:
    as-method => true
    default => true
  params:Maybe<SchottkyDiodeSymbolParams> with:
    as-method => true
    default => None()

with:
  printer => true
  keyword-constructor => true


public defmethod name (x:SchottkyDiodeSymbol) -> String :
  "SchottkyDiode"

defmethod get-params (x:SchottkyDiodeSymbol) -> SchottkyDiodeSymbolParams :
  match(params(x)):
    (_:None): get-default-schottky-diode-symbol-params()
    (given:One<SchottkyDiodeSymbolParams>): value(given)

public defmethod build-artwork (
  x:SchottkyDiodeSymbol, sn:SymbolNode
  ):
  val p = get-params(x) as SchottkyDiodeSymbolParams
  build-schottky-diode-glyphs(sn, pitch(x), p)

var CURR-SCHOTTKY-DIODE-SYMBOL:TwoPinSymbol = SchottkyDiodeSymbol()
public defn get-default-schottky-diode-symbol () : CURR-SCHOTTKY-DIODE-SYMBOL
public defn set-default-schottky-diode-symbol (symb:TwoPinSymbol) :
  CURR-SCHOTTKY-DIODE-SYMBOL = symb


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Zener Diode
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val DEF_ZENER_WING_SIZE = 0.1

public defstruct ZenerDiodeSymbolParams <: DiodeSymbolParams :
  wing-size:Double with:
    ensure => ensure-positive!,
    updater => sub-wing-size
    default => DEF_ZENER_WING_SIZE
  body-dims:Dims|Double with:
    ensure => ensure-positive!,
    updater => sub-body-dims
    as-method => true
    default => DEF_BODY_WIDTH
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width,
    as-method => true
    default => DEF_LINE_WIDTH
  filled?:True|False with:
    updater => sub-filled?,
    as-method => true
    default => DEF_FILLED?
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()
with:
  keyword-constructor => true
  equalable => true
  printer => true


public defn sub-base-params (p:ZenerDiodeSymbolParams, b:DiodeSymbolParams) -> ZenerDiodeSymbolParams :
  p $> sub-body-dims{_, body-dims(b)}
    $> sub-line-width{_, line-width(b)}
    $> sub-filled?{_, filled?(b)}
    $> {_ as ZenerDiodeSymbolParams}

var CUSTOM_ZENER_PARAMS:True|False = false
var DEF_ZENER_DIODE_PARAMS = ZenerDiodeSymbolParams()
public defn get-default-zener-diode-symbol-params () -> ZenerDiodeSymbolParams :
  if CUSTOM_ZENER_PARAMS:
    DEF_ZENER_DIODE_PARAMS
  else:
    sub-base-params(DEF_ZENER_DIODE_PARAMS, get-default-diode-symbol-params())


doc: \<DOC>
Determine if the new params for the zener diode symbol modifies the base parameters

This helps implement the logic that allows the user to modify
only the `DEF_NON_POL_PARAMS` and have this propagate to
the `DEF_POL_PARAMS`
<DOC>
defn check-non-base-defaults (p:ZenerDiodeSymbolParams) -> True|False :
  val b = get-default-diode-symbol-params()
  (p as DiodeSymbolParams) != b

public defn set-default-zener-diode-symbol-params (v:ZenerDiodeSymbolParams) -> False :
  CUSTOM_ZENER_PARAMS = check-non-base-defaults(v)
  DEF_ZENER_DIODE_PARAMS = v


public defn build-zener-diode-glyphs (
  node:SymbolNode,
  pitch:Double,
  params:ZenerDiodeSymbolParams
  ) :
  build-diode-glyphs(node, pitch, params as DiodeSymbolParams)

  val [body-dims, line-width, filled?] = to-tuple(params)
  val wing-size = wing-size(params)

  val bw2 = body-width(params) / 2.0
  val h = compute-body-start(params)

  val wing-shape = Line(line-width, [Point(0.0, 0.0), Point((- wing-size), (- wing-size))])

  val L-wing = loc((- bw2), (- h)) * wing-shape
  add-glyph(node, L-wing, name? = One("L-wing"))
  val R-wing = loc(bw2, (- h), 180.0) * wing-shape
  add-glyph(node, R-wing, name? = One("R-wing"))


public defstruct ZenerDiodeSymbol <: TwoPinSymbol :
  doc: \<DOC>
  Pitch between the Pins of a Diode Symbol
  This value is in symbol grid units (not mm)
  <DOC>
  pitch:Double with:
    as-method => true
    default => TWO_PIN_DEF_PITCH
  polarized?:True|False with:
    as-method => true
    default => true
  params:Maybe<ZenerDiodeSymbolParams> with:
    as-method => true
    default => None()
with:
  printer => true
  keyword-constructor => true

public defmethod name (x:ZenerDiodeSymbol) -> String :
  "ZenerDiode"

defmethod get-params (x:ZenerDiodeSymbol) -> ZenerDiodeSymbolParams :
  match(params(x)):
    (_:None): get-default-zener-diode-symbol-params()
    (given:One<ZenerDiodeSymbolParams>): value(given)


public defmethod build-artwork (
  x:ZenerDiodeSymbol, sn:SymbolNode
  ):
  val p = get-params(x) as ZenerDiodeSymbolParams
  build-zener-diode-glyphs(sn, pitch(x), p)

var CURR-ZENER-DIODE-SYMBOL:TwoPinSymbol = ZenerDiodeSymbol()
public defn get-default-zener-diode-symbol () : CURR-ZENER-DIODE-SYMBOL
public defn set-default-zener-diode-symbol (symb:TwoPinSymbol) :
  CURR-ZENER-DIODE-SYMBOL = symb


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Tunnel Diode
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
val DEF_TUNNEL_WING_SIZE = 0.1

public defstruct TunnelDiodeSymbolParams <: DiodeSymbolParams :
  wing-size:Double with:
    ensure => ensure-positive!
    updater => sub-wing-size
    default => DEF_TUNNEL_WING_SIZE
  body-dims:Dims|Double with:
    ensure => ensure-positive!
    updater => sub-body-dims
    as-method => true
    default => DEF_BODY_WIDTH
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width
    as-method => true
    default => DEF_LINE_WIDTH
  filled?:True|False with:
    updater => sub-filled?
    as-method => true
    default => DEF_FILLED?
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()
with:
  keyword-constructor => true
  equalable => true
  printer => true

public defn sub-base-params (p:TunnelDiodeSymbolParams, b:DiodeSymbolParams) -> TunnelDiodeSymbolParams :
  p $> sub-body-dims{_, body-dims(b)}
    $> sub-line-width{_, line-width(b)}
    $> sub-filled?{_, filled?(b)}
    $> {_ as TunnelDiodeSymbolParams}

var CUSTOM_TUNNEL_PARAMS:True|False = false
var DEF_TUNNEL_DIODE_PARAMS = TunnelDiodeSymbolParams()
public defn get-default-tunnel-diode-symbol-params () -> TunnelDiodeSymbolParams :
  if CUSTOM_TUNNEL_PARAMS:
    DEF_TUNNEL_DIODE_PARAMS
  else:
    sub-base-params(DEF_TUNNEL_DIODE_PARAMS, get-default-diode-symbol-params())


doc: \<DOC>
Determine if the new params for the tunnel diode symbol modifies the base parameters
<DOC>
defn check-non-base-defaults (p:TunnelDiodeSymbolParams) -> True|False :
  val b = get-default-diode-symbol-params()
  (p as DiodeSymbolParams) != b

public defn set-default-tunnel-diode-symbol-params (v:TunnelDiodeSymbolParams) -> False :
  CUSTOM_TUNNEL_PARAMS = check-non-base-defaults(v)
  DEF_TUNNEL_DIODE_PARAMS = v

public defn build-tunnel-diode-glyphs (
  node:SymbolNode,
  pitch:Double,
  params:TunnelDiodeSymbolParams
  ) :
  build-diode-glyphs(node, pitch, params as DiodeSymbolParams)

  val [body-dims, line-width, filled?] = to-tuple(params)
  val wing-size = wing-size(params)

  val bw2 = body-width(params) / 2.0
  val h = compute-body-start(params)

  val wing-shape = Line(line-width, [Point(0.0, 0.0), Point(0.0, wing-size)])

  val L-wing = loc((- bw2), (- h)) * wing-shape
  add-glyph(node, L-wing, name? = One("L-wing"))
  val R-wing = loc(bw2, (- h)) * wing-shape
  add-glyph(node, R-wing, name? = One("R-wing"))


public defstruct TunnelDiodeSymbol <: TwoPinSymbol :
  doc: \<DOC>
  Pitch between the Pins of a Diode Symbol
  This value is in symbol grid units (not mm)
  <DOC>
  pitch:Double with:
    as-method => true
    default => TWO_PIN_DEF_PITCH
  polarized?:True|False with:
    as-method => true
    default => true
  params:Maybe<TunnelDiodeSymbolParams> with:
    as-method => true
    default => None()
with:
  printer => true
  keyword-constructor => true

public defmethod name (x:TunnelDiodeSymbol) -> String :
  "TunnelDiode"

defmethod get-params (x:TunnelDiodeSymbol) -> TunnelDiodeSymbolParams:
  match(params(x)):
    (_:None): get-default-tunnel-diode-symbol-params()
    (given:One<TunnelDiodeSymbolParams>): value(given)

public defmethod build-artwork (
  x:TunnelDiodeSymbol, sn:SymbolNode
  ):
  val p = get-params(x) as TunnelDiodeSymbolParams
  build-tunnel-diode-glyphs(sn, pitch(x), p)

var CURR-TUNNEL-DIODE-SYMBOL:TwoPinSymbol = TunnelDiodeSymbol()
public defn get-default-tunnel-diode-symbol () : CURR-TUNNEL-DIODE-SYMBOL
public defn set-default-tunnel-diode-symbol (symb:TwoPinSymbol) :
  CURR-TUNNEL-DIODE-SYMBOL = symb


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Light Emitting Diode (LED)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


public defstruct LEDSymbolParams <: DiodeSymbolParams :

  arrow-params:ArrowSymbolParams with:
    updater => sub-arrow-params
  body-dims:Dims|Double with:
    ensure => ensure-positive!,
    updater => sub-body-dims
    as-method => true
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width,
    as-method => true
  filled?:True|False with:
    updater => sub-filled?,
    as-method => true
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()

with:
  constructor => #LEDSymbolParams
  equalable => true
  printer => true

public defn compute-default-arrows (body-dims:Double|Dims, line-width:Double) -> ArrowSymbolParams :
  defn compute-defaults (w:Double) -> [Double, Double] :
    val l = w * (50.0 %)
    val h = w * (20.0 %)
    [h, l]

  val [h, l] = match(body-dims):
    (w:Double): compute-defaults(w)
    (d:Dims): compute-defaults $ x(d)

  ArrowSymbolParams(
    head-dims = Dims(h, h),
    shaft-length = l,
    line-width = line-width
  )

public defn LEDSymbolParams (
  --
  body-dims:Dims|Double = DEF_BODY_WIDTH
  line-width:Double = DEF_LINE_WIDTH
  arrow-params:ArrowSymbolParams = compute-default-arrows(body-dims, line-width)
  filled?:True|False = DEF_FILLED?
) -> LEDSymbolParams :
  #LEDSymbolParams(arrow-params, body-dims, line-width, filled?)

public defn sub-base-params (p:LEDSymbolParams, b:DiodeSymbolParams) -> LEDSymbolParams :
  p $> sub-body-dims{_, body-dims(b)}
    $> sub-line-width{_, line-width(b)}
    $> sub-filled?{_, filled?(b)}
    $> {_ as LEDSymbolParams}

var CUSTOM_LED_PARAMS:True|False = false
var DEF_LED_PARAMS = LEDSymbolParams()
public defn get-default-led-symbol-params () -> LEDSymbolParams :
  if CUSTOM_LED_PARAMS:
    DEF_LED_PARAMS
  else:
    sub-base-params(DEF_LED_PARAMS, get-default-diode-symbol-params())


doc: \<DOC>
Determine if the new params for the tunnel diode symbol modifies the base parameters
<DOC>
defn check-non-base-defaults (p:LEDSymbolParams) -> True|False :
  val b = get-default-diode-symbol-params()
  (p as DiodeSymbolParams) != b

public defn set-default-led-symbol-params (v:LEDSymbolParams) -> False :
  CUSTOM_LED_PARAMS = check-non-base-defaults(v)
  DEF_LED_PARAMS = v

public defn build-led-glyphs (node:SymbolNode, pitch:Double, params:LEDSymbolParams) :
  build-diode-glyphs(node, pitch, params as DiodeSymbolParams)

  val arrow-params = arrow-params(params)
  val arrow = construct-arrow(arrow-params)

  val bw2 = body-width(params) / 2.0
  val h = compute-body-start(params)
  val angle = 30.0

  val shaft = shaft-length(arrow-params)
  val x-off = (- (bw2 + (1.2 * shaft)))
  val y-off = (h / -2.0)
  val y-adj = 1.5 * y-off
  val arrow-1 = loc(x-off, y-off , angle) * arrow
  add-glyph(node, arrow-1, name? = One("arrow-1"))
  val arrow-2 = loc(x-off, (y-off + y-adj), angle) * arrow
  add-glyph(node, arrow-2, name? = One("arrow-2"))


public defstruct LEDSymbol <: TwoPinSymbol :
  doc: \<DOC>
  Pitch between the Pins of a Diode Symbol
  This value is in symbol grid units (not mm)
  <DOC>
  pitch:Double with:
    as-method => true
    default => TWO_PIN_DEF_PITCH
  polarized?:True|False with:
    as-method => true
    default => true
  params:Maybe<LEDSymbolParams> with:
    as-method => true
    default => None()
with:
  printer => true
  keyword-constructor => true

public defmethod name (x:LEDSymbol) -> String :
  "LED"

defmethod get-params (x:LEDSymbol) -> LEDSymbolParams:
  match(params(x)):
    (_:None): get-default-led-symbol-params()
    (given:One<LEDSymbolParams>): value(given)

public defmethod build-artwork (
  x:LEDSymbol, sn:SymbolNode
  ):
  val p = get-params(x) as LEDSymbolParams
  build-led-glyphs(sn, pitch(x), p)

var CURR-LED-SYMBOL:TwoPinSymbol = LEDSymbol()
public defn get-default-led-symbol () : CURR-LED-SYMBOL
public defn set-default-led-symbol (symb:TwoPinSymbol) :
  CURR-LED-SYMBOL = symb


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; PhotoDiode
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

public defstruct PhotoDiodeSymbolParams <: DiodeSymbolParams :

  arrow-params:ArrowSymbolParams with:
    updater => sub-arrow-params
  body-dims:Dims|Double with:
    ensure => ensure-positive!,
    updater => sub-body-dims
    as-method => true
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width,
    as-method => true
  filled?:True|False with:
    updater => sub-filled?,
    as-method => true
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()
with:
  constructor => #PhotoDiodeSymbolParams
  equalable => true
  printer => true

public defn PhotoDiodeSymbolParams (
  --
  body-dims:Dims|Double = DEF_BODY_WIDTH
  line-width:Double = DEF_LINE_WIDTH
  arrow-params:ArrowSymbolParams = compute-default-arrows(body-dims, line-width)
  filled?:True|False = DEF_FILLED?
) -> PhotoDiodeSymbolParams :
  #PhotoDiodeSymbolParams(arrow-params, body-dims, line-width, filled?)

public defn sub-base-params (p:PhotoDiodeSymbolParams, b:DiodeSymbolParams) -> PhotoDiodeSymbolParams :
  p $> sub-body-dims{_, body-dims(b)}
    $> sub-line-width{_, line-width(b)}
    $> sub-filled?{_, filled?(b)}
    $> {_ as PhotoDiodeSymbolParams}

var CUSTOM_PHOTO_PARAMS:True|False = false
var DEF_PHOTO_DIODE_PARAMS = PhotoDiodeSymbolParams()
public defn get-default-photo-diode-symbol-params () -> PhotoDiodeSymbolParams :
  if CUSTOM_PHOTO_PARAMS:
    DEF_PHOTO_DIODE_PARAMS
  else:
    sub-base-params(DEF_PHOTO_DIODE_PARAMS, get-default-diode-symbol-params())


doc: \<DOC>
Determine if the new params for the tunnel diode symbol modifies the base parameters
<DOC>
defn check-non-base-defaults (p:PhotoDiodeSymbolParams) -> True|False :
  val b = get-default-diode-symbol-params()
  (p as DiodeSymbolParams) != b

public defn set-default-photo-diode-symbol-params (v:PhotoDiodeSymbolParams) -> False :
  CUSTOM_PHOTO_PARAMS = check-non-base-defaults(v)
  DEF_PHOTO_DIODE_PARAMS = v

public defn build-photo-diode-glyphs (node:SymbolNode, pitch:Double, params:PhotoDiodeSymbolParams) :
  build-diode-glyphs(node, pitch, params as DiodeSymbolParams)

  val arrow-params = arrow-params(params)
  val arrow = construct-arrow(arrow-params)

  val bw2 = body-width(params) / 2.0
  val h = compute-body-start(params)

  val angle = (150.0)

  val x-off = (- (1.2 * bw2)  )
  val y-off = (h / -2.0)
  val y-adj = 0.3 * y-off
  val arrow-1 = loc(x-off, y-adj + y-off, angle) * arrow
  add-glyph(node, arrow-1, name? = One("arrow-1"))
  val arrow-2 = loc(x-off, (y-adj - y-off), angle) * arrow
  add-glyph(node, arrow-2, name? = One("arrow-2"))


public defstruct PhotoDiodeSymbol <: TwoPinSymbol :
  doc: \<DOC>
  Pitch between the Pins of a Diode Symbol
  This value is in symbol grid units (not mm)
  <DOC>
  pitch:Double with:
    as-method => true
    default => TWO_PIN_DEF_PITCH
  polarized?:True|False with:
    as-method => true
    default => true
  params:Maybe<PhotoDiodeSymbolParams> with:
    as-method => true
    default => None()
with:
  printer => true
  keyword-constructor => true


public defmethod name (x:PhotoDiodeSymbol) -> String :
  "PhotoDiode"

defmethod get-params (x:PhotoDiodeSymbol) -> PhotoDiodeSymbolParams:
  match(params(x)):
    (_:None): get-default-photo-diode-symbol-params()
    (given:One<PhotoDiodeSymbolParams>): value(given)

public defmethod build-artwork (
  x:PhotoDiodeSymbol, sn:SymbolNode
  ):
  val p = get-params(x) as PhotoDiodeSymbolParams
  build-photo-diode-glyphs(sn, pitch(x), p)

var CURR-PHOTO-DIODE-SYMBOL:TwoPinSymbol = PhotoDiodeSymbol()
public defn get-default-photo-diode-symbol () : CURR-PHOTO-DIODE-SYMBOL
public defn set-default-photo-diode-symbol (symb:TwoPinSymbol) :
  CURR-PHOTO-DIODE-SYMBOL = symb



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; TVS Diode
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val DEF_TVS_WING_SIZE = 0.1

public defstruct TVSDiodeSymbolParams <: DiodeSymbolParams :
  wing-size:Double with:
    ensure => ensure-positive!,
    updater => sub-wing-size
    default => DEF_TVS_WING_SIZE
  body-dims:Dims|Double with:
    ensure => ensure-positive!,
    updater => sub-body-dims
    as-method => true
    default => DEF_BODY_WIDTH
  line-width:Double with:
    ensure => ensure-positive!
    updater => sub-line-width,
    as-method => true
    default => DEF_LINE_WIDTH
  filled?:True|False with:
    updater => sub-filled?,
    as-method => true
    default => DEF_FILLED?
  ; SymbolParams Interface
  label-params?:Maybe<SymbolLabelParams> with:
    as-method => true
    default => None()
with:
  keyword-constructor => true
  equalable => true
  printer => true


public defn sub-base-params (p:TVSDiodeSymbolParams, b:DiodeSymbolParams) -> TVSDiodeSymbolParams :
  p $> sub-body-dims{_, body-dims(b)}
    $> sub-line-width{_, line-width(b)}
    $> sub-filled?{_, filled?(b)}
    $> {_ as TVSDiodeSymbolParams}

var CUSTOM_TVS_PARAMS:True|False = false
var DEF_TVS_DIODE_PARAMS = TVSDiodeSymbolParams()
public defn get-default-tvs-diode-symbol-params () -> TVSDiodeSymbolParams :
  if CUSTOM_TVS_PARAMS:
    DEF_TVS_DIODE_PARAMS
  else:
    sub-base-params(DEF_TVS_DIODE_PARAMS, get-default-diode-symbol-params())


doc: \<DOC>
Determine if the new params for the tvs diode symbol modifies the base parameters

This helps implement the logic that allows the user to modify
only the `DEF_NON_POL_PARAMS` and have this propagate to
the `DEF_POL_PARAMS`
<DOC>
defn check-non-base-defaults (p:TVSDiodeSymbolParams) -> True|False :
  val b = get-default-diode-symbol-params()
  (p as DiodeSymbolParams) != b

public defn set-default-tvs-diode-symbol-params (v:TVSDiodeSymbolParams) -> False :
  CUSTOM_TVS_PARAMS = check-non-base-defaults(v)
  DEF_TVS_DIODE_PARAMS = v


public defn build-tvs-diode-glyphs (
  node:SymbolNode,
  pitch:Double,
  params:TVSDiodeSymbolParams
  ) :

  ; The TVS Diode Symbol consists of 2 triangles, one pointed down
  ;   and the other pointed up.
  ;
  ;
  ;     ---------
  ;     \       /
  ;      \     /
  ;       \   /
  ;        \ /   /
  ;      -------
  ;    /   / \
  ;       /   \
  ;      /     \
  ;     /       \
  ;    -----------


  val [body-dims, line-width, filled?] = to-tuple(params)
  val wing-size = wing-size(params)

  val bw2 = body-width(params) / 2.0
  val h = compute-body-start(params)
  val h2 = h / 2.0
  val p2 = pitch / 2.0

  line(node, [Point(0.0, (- p2)), Point(0.0, (- h))], width = line-width, name = "front-porch")

  val tri-pts = [Point(0.0, (- h2)), Point(bw2, h2), Point((- bw2), h2), Point(0.0, (- h2))]
  val half-Tri = if filled?:
    Polygon([tri-pts])
  else:
    Line(line-width, tri-pts)

  val top-Tri = loc(0.0, h2 ) * half-Tri
  add-glyph(node, top-Tri, name? = One("body"))
  val bot-Tri = loc(0.0, (- h2), 180.0) * half-Tri
  add-glyph(node, bot-Tri, name? = One("body"))

  line(node, [Point(0.0, h), Point(0.0, p2)], width = line-width, name = "back-porch")

  val wing-shape = Line(line-width, [Point(bw2, 0.0), Point(0.0, 0.0), Point((- wing-size), (- wing-size))])

  val L-wing = loc((- bw2), 0.0) * wing-shape
  add-glyph(node, L-wing, name? = One("L-wing"))
  val R-wing = loc(bw2, 0.0, 180.0) * wing-shape
  add-glyph(node, R-wing, name? = One("R-wing"))


public defstruct TVSDiodeSymbol <: TwoPinSymbol :
  doc: \<DOC>
  Pitch between the Pins of a Diode Symbol
  This value is in symbol grid units (not mm)
  <DOC>
  pitch:Double with:
    as-method => true
    default => TWO_PIN_DEF_PITCH
  polarized?:True|False with:
    as-method => true
    default => true
  params:Maybe<TVSDiodeSymbolParams> with:
    as-method => true
    default => None()
with:
  printer => true
  keyword-constructor => true

public defmethod name (x:TVSDiodeSymbol) -> String :
  "TVSDiode"

defmethod get-params (x:TVSDiodeSymbol) -> TVSDiodeSymbolParams :
  match(params(x)):
    (_:None): get-default-tvs-diode-symbol-params()
    (given:One<TVSDiodeSymbolParams>): value(given)


public defmethod build-artwork (
  x:TVSDiodeSymbol, sn:SymbolNode
  ):
  val p = get-params(x) as TVSDiodeSymbolParams
  build-tvs-diode-glyphs(sn, pitch(x), p)

var CURR-TVS-DIODE-SYMBOL:TwoPinSymbol = TVSDiodeSymbol()
public defn get-default-tvs-diode-symbol () : CURR-TVS-DIODE-SYMBOL
public defn set-default-tvs-diode-symbol (symb:TwoPinSymbol) :
  CURR-TVS-DIODE-SYMBOL = symb