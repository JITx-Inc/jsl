#use-added-syntax(jitx)
defpackage jsl/circuits/CrystalResonator:
  import core
  import jitx
  import jitx/commands
  import jitx/parts/query-api

  import jsl/design/E-Series
  import jsl/design/introspection

doc: \<DOC>
Construct a Crystal Resonator Circuit

@param xtal-type Crystal that will be instantiated and connected with load capacitors
@param C-load Load Capacitance of the Crystal.
@param C-stray Stray capacitance in the traces and input/output pins of the IC oscillator
that this circuit will attach to.
@param V-rating Minimum voltage rating for the capacitors selected for this circuit. The
user is assumed to have added the necessary voltage derating for their application.
@param tc-code Default temperature rating to use for the load capacitors. This overides
the default temperature coefficient code from the C-query. The default value is `C0G`
@param prec Capacitor Value Precision Series that load capacitors will be selected from.
@param C-query Capacitor Query Object for customizing capacitor selection. Default is `get-default-capacitor-query()`.
The following keys will be modified:

1.  `temperature-coefficient_code` - This function uses `C0G`
2.  `rated-voltage` - This function overrides any voltage rating with the `V-rating` value. The results are sorted from lowest to highest.

@return A `pcb-module` definition that can be instantiated to form the
resonant tank for a crystal resonator. This module will have the following ports:

*  `p[[1, 2]]` - Crystal connections
*  `COMMON` - Common reference, typically ground.
<DOC>
public defn create-crystal-resonator (
  --
  xtal-type:Instantiable,
  C-load:Toleranced,
  C-stray:Toleranced,
  V-rating:Double,
  tc-code:String = "C0G",
  prec:Percentage = (5 %),
  C-query:CapacitorQuery = get-default-capacitor-query()
  ) -> Instantiable:

  val cap-V = 2.0 * (C-load - C-stray)
  val c-bal = closest-std-val(typ(cap-V), prec)

  val q = add(
    C-query,
    rated-voltage = AtLeast(V-rating),
    sort! = SortKey(rated-voltage = Increasing),
    temperature-coefficient_code = tc-code
  )

  val cap-type = create-capacitor(
    q,
    capacitance = c-bal
    precision = prec
  )

  pcb-module crystal-resonator-mod:

    port p : pin[[1,2]]
    port COMMON

    inst xtal : xtal-type
    for i in indices(xtal.p) do:
      net (p[i], xtal.p[i])

    ; check for case pins on xtal
    val case-pt? = get-port-by-name(xtal, "case")
    match(case-pt?):
      (_:None): false
      (given:One<JITXObject>):
        val case-pt = value(given)
        for i in indices(case-pt) do:
          net (case-pt[i], COMMON)

    ; Instantiate the capacitors
    inst load-caps : cap-type[2]
    for (i in indices(xtal.p), j in 0 to false) do:
      net (load-caps[j].p[1], xtal.p[i])
      short-trace(load-caps[j].p[1], xtal.p[i])
      net (load-caps[j].p[2], COMMON)


  crystal-resonator-mod

