; TODO - Most geometry is defined in JITX esir.stanza
;   why is this in JSL/OCDB ? Should this move to JITX proper.
#use-added-syntax(jitx)
defpackage jsl/geometry/LineRectangle:
  import core
  import jitx
  import jitx/commands

defn gen-default-width ():
  clearance(current-rules(), MinSilkscreenWidth)

val DEF_LR_POSE = loc(0.0, 0.0)

doc: \<DOC>
Construct a rectangular outline with a `Line` element
@param d Dimensions of the Rectangle - x = width, y = height
@param line-width Width of the line in mm
@param pose Optional transformation for the rectangle. The
default is `loc(0.0, 0.0)`
@param anchor Defines the origin of the LineRectangle before
the `pose` transformation is applied. Default value is `C` or
center aligned vertically and horizontally.
<DOC>
public defn LineRectangle (
  d:Dims
  --
  line-width:Double = gen-default-width(),
  pose:Pose = DEF_LR_POSE,
  anchor:Anchor = C
  ):
  val [vt, hr] = components(anchor)
  val xc = match(hr):
    (hr:W): x(d) / 2.0
    (hr:C): 0.0
    (hr:E): x(d) / -2.0
  val yc = match(vt):
    (vt:S): y(d) / 2.0
    (vt:C): 0.0
    (vt:N): y(d) / -2.0
  val [w2, h2] = [x(d) / 2.0, y(d) / 2.0]
  pose * Line(line-width, [
    Point(xc - w2, yc - h2)
    Point(xc + w2, yc - h2)
    Point(xc + w2, yc + h2)
    Point(xc - w2, yc + h2)
    Point(xc - w2, yc - h2)])

doc: \<DOC>
Construct a rectangular outline with a `Line` element

@param width Width (X-dimension) of the generated rectangle
in the default coordinate frame before the `pose` transformation
is applied.
@param height Height (Y-dimension) of the generated rectangle
in the default coordinate frame before the `pose` transformation
is applied.

@param line-width Width of the line in mm
@param pose Optional transformation for the rectangle. The
default is `loc(0.0, 0.0)`
@param anchor Defines the origin of the LineRectangle before
the `pose` transformation is applied. Default value is `C` or
center aligned vertically and horizontally.
<DOC>
public defn LineRectangle (
  width:Double, height:Double,
  --
  line-width:Double = gen-default-width(),
  pose:Pose = DEF_LR_POSE,
  anchor:Anchor = C
  ):
  LineRectangle(
    Dims(width, height),
    line-width = line-width,
    pose = pose,
    anchor = anchor
    )

