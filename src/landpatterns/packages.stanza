doc: \<DOC>
@title Packages & Landpatterns
@brief Interface definitions for Landpatterns
@desc

This package contains the definition of some abstract types for
defining the landpatterns of various components.

The goal of these definitions is to provide a consistent interface
across all landpattern definitions. This promotes re-use and
correctness.

All of the entities in this package use millimeters as their standard unit.

TODO - Diagram here discussing the concept for landpatterns & component packages.
  We should include examples of various packages and use the right nomenclature
  to describe things.

<DOC>
#use-added-syntax(jitx)
defpackage jsl/landpatterns/packages:
  import core
  import jitx
  import jitx/commands

  import jsl/landpatterns/helpers
  import jsl/design/settings
  import jsl/landpatterns/pad-planner
  import jsl/landpatterns/numbering
  import jsl/landpatterns/courtyard
  import jsl/landpatterns/VirtualLP
  import jsl/geometry/basics with:
    prefix => jsl-

doc: \<DOC>
Physical body of the Component (also called the Lead Carrier or just Carrier)

For Through-hole resistors - this is the ceramic cylinder, not the leads
For SMT resistors, the body is basically the whole chip resistor.

For QFNs, the body is the entire package
For QFPs, the body is the ceramic or plastic but not the leads.

TODO - Figure here
<DOC>
public defstruct PackageBody <: Hashable & Equalable :
  doc: \<DOC>
  Measure of the package body in the X dimension.

  Examples:
  - Typically for SOP/SOIC/DFN/SON, this dimension is in the same direction as the lead-span
  - For non-square QFN/QFP/BGA packages, this is typically the shorter of the two
  dimensions.
  - For axial through-hole components, the width is typically the diameter of
  the device cylinder and orthogonal to the axial leads.
  - For radial through-hole components:
    -  For cylindrical components, the width is equal to the length
    -  For non-cylindrical components, the width is typically the shorter of the two dimensions and approximately orthogonal to the plane of the leads.
  <DOC>
  width:Toleranced with: (ensure => ensure-positive!)
  doc: \<DOC>
  Measure of the package body in the Y dimension.

  Examples:
  - Typically for SOP/SOIC/DFN/SON, this dimension is orthogonal to the lead-span.
  - For non-square QFN/QFP/BGA packages, this is typically the longer of the two
  dimensions.
  - For axial through-hole components, the length is typically parallel to the
  axial leads of the device.
  - For radial through-hole components:
    -  For cylindrical components, the length is equal to the width.
    -  For non-cylindrical components, the length is typically the longer dimension and parallel to the leads of the device.
  <DOC>
  length:Toleranced with: (ensure => ensure-positive!)
  doc: \<DOC>
  Measure of the package body in the Z dimension

  This dimension of the package body is the nominal distance that
  the component will stand off the surface of the PCB. This
  dimension will typically not effect the layout and routing of the
  PCB from an electrical perspective but will likely effect the
  mechanical integration of the component.
  <DOC>
  height:Toleranced with: (ensure => ensure-positive!)

  doc: \<DOC>
  Transform applied to the package body.
  By default the package body is centered at (0.0, 0.0)
  <DOC>
  pose:Pose with: (
    default => loc(0.0, 0.0)
  )
with:
  equalable => true
  hashable => true
  printer => true
  constructor => #PackageBody

doc: \<DOC>
Create a Package Body as a rectangular cuboid.
<DOC>
public defn PackageBody (
  --
  width:Toleranced,
  length:Toleranced,
  height:Toleranced,
  pose:Pose = loc(0.0, 0.0)
  ):
  #PackageBody(width, length, height, pose)

doc: \<DOC>
Physical Envelope of the package body.
<DOC>
public defmulti envelope (x:PackageBody) -> Dims

public defmethod envelope (x:PackageBody) -> Dims :
  Dims(max-value(width(x)), max-value(length(x)))


doc: \<DOC>
Every component has a representation for a
package
<DOC>
public deftype Package <: Hashable

doc: \<DOC>
IPC7351 compliant name for the package generated
by this instance.
<DOC>
public defmulti name (x:Package) -> String

doc: \<DOC>
Number of Total Lead Mounts on this Package

This number should include all possible standard lead
locations on this package.

1.  The user should not substract the pins that are marked absent.
2.  The user should not include the thermal lead if any.
<DOC>
public defmulti num-leads (x:Package) -> Int

doc: \<DOC>
Package Body provides the physical envelope for
the package's body.
<DOC>
public defmulti package-body (x:Package) -> PackageBody

doc: \<DOC>
Pad Planner for this Package
<DOC>
public defmulti pad-planner (x:Package) -> PadPlanner

doc: \<DOC>
Lead Numbering Scheme for this Package
<DOC>
public defmulti lead-numbering (x:Package) -> Numbering

doc: \<DOC>
Default Landpattern Generator Pose
This value should be the default value used for the
`pose` argument in all `make-landpattern` invokations.
<DOC>
public val DEF_LP_POSE = loc(0.0, 0.0)


doc: \<DOC>
Build the pad/copper in a virtual landpattern

This function is used to build the landpattern's
pad definitions in a virtual landpattern environment.
That virtual environment can then be written out to a
`pcb-landpattern` definition.

@param pkg Package definition to build
@param vp Virtual Landpattern Container
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti build-landpattern (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = ?
  ) -> False


doc: \<DOC>
Generator for creating the copper pad landpattern

This generator is typically called within a `pcb-landpattern`
definition to create the necessary pads and copper geometry associated
with that component's landpattern.

@param pkg IC Package for which we are creating a landpattern
@param pose Offset to apply to all of the geometry generated
by this generator function. By default, no offset is applied.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti make-landpattern (
  pkg:Package
  --
  pose:Pose = ?
  density-level:DensityLevel = ?
  )

doc: \<DOC>
Default Implementation of the Landpattern Copper Generator
<DOC>
public defmethod make-landpattern (
  pkg:Package
  --
  pose:Pose = DEF_LP_POSE
  density-level:DensityLevel = DENSITY-LEVEL
  ):
  inside pcb-landpattern:
    val virt = VirtualLP(pose)
    build-landpattern(pkg, virt, density-level = density-level)
    make-landpattern(virt)


doc: \<DOC>
Create silkscreen content in virtual landpattern

This function is used to build the landpattern's
silkscreen artwork in a virtual landpattern environment.
That virtual environment can then be written out to a
`pcb-landpattern` definition.

@param pkg Package definition to build
@param vp Virtual Landpattern Container
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti build-silkscreen (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = ?
  ) -> False

doc: \<DOC>
Default silkscreen creator - No Silkscreen Content
<DOC>
public defmethod build-silkscreen (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = DENSITY-LEVEL
  ) -> False:
  false

doc: \<DOC>
Generator for creating the silkscreen of a landpattern

This generator is typically called within a `pcb-landpattern`
definition to create the silkscreen outline, pin-1 indicator, or
any other silkscreen artwork.

@param pkg IC Package for which we are creating silkscreen artwork
@param pkg pose Offset to apply to all of the geometry generated
by this generator function. By default, no offset is applied.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti make-silkscreen (
  pkg:Package
  --
  pose:Pose = ?
  density-level:DensityLevel = ?
  )

doc: \<DOC>
Default Silkscreen Implementation - No Silkscreen Artwork Created
<DOC>
public defmethod make-silkscreen (
  pkg:Package
  --
  pose:Pose = DEF_LP_POSE
  density-level:DensityLevel = DENSITY-LEVEL
  ):
  inside pcb-landpattern:
    val virt = VirtualLP(pose)
    build-silkscreen(pkg, virt, density-level = density-level)
    make-landpattern(virt)



doc: \<DOC>
Create courtyard content in a virtual landpattern

This function is used to build the landpattern's
courtyard region in a virtual landpattern environment.
That virtual environment can then be written out to a
`pcb-landpattern` definition.

@param pkg Package definition to build
@param vp Virtual Landpattern Container
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti build-courtyard (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = ?
  ) -> False

doc: \<DOC>
Default courtyard creator

This function creates the default courtyard for the component
by introducing a center mark at the origin and a bounding
rectangle on the `Courtyard(Top)` layer.
<DOC>
public defmethod build-courtyard (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = DENSITY-LEVEL
  ) -> False:
  build-courtyard-origin(vp)
  ; @TODO - I need some way to extract the courtyard excess specifications
  ;  from the component
  build-courtyard-boundary(vp)


doc: \<DOC>
Generator for creating courtyard for a landpattern

This generator is typically called within a `pcb-landpattern`
definition to create the courtyard outline for this component.

@param pkg IC Package for which we are creating silkscreen artwork
@param pkg pose Offset to apply to all of the geometry generated
by this generator function. By default, no offset is applied.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti make-courtyard (
  pkg:Package
  --
  pose:Pose = ?
  density-level:DensityLevel = ?
  )

public defmethod make-courtyard (
  pkg:Package
  --
  pose:Pose = DEF_LP_POSE
  density-level:DensityLevel = DENSITY-LEVEL
  ):
  inside pcb-landpattern:
    val virt = VirtualLP(pose)
    build-courtyard(pkg, virt, density-level = density-level)
    make-landpattern(virt)


doc: \<DOC>
Create `keep-out` regions in a virtual landpattern

This function is used to build the landpattern's
keep-out regions in a virtual landpattern environment.
This may include keep-outs for copper, vias, etc.
That virtual environment can then be written out to a
`pcb-landpattern` definition.

@param pkg Package definition to build
@param vp Virtual Landpattern Container
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti build-keep-out (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = ?
  ) -> False

doc: \<DOC>
Default `build-keep-out` - No Keep-out Regions Applied.
<DOC>
public defmethod build-keep-out (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = DENSITY-LEVEL
  ) -> False:
  false

doc: \<DOC>
Generator for creating keep-out constraints for a landpattern

This generator is typically called within a `pcb-landpattern`
definition to create the keep-out regions for this component.

@param pkg IC Package for which we are creating silkscreen artwork
@param pkg pose Offset to apply to all of the geometry generated
by this generator function. By default, no offset is applied.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti make-keep-out (
  pkg:Package
  --
  pose:Pose = ?
  density-level:DensityLevel = ?
  )

doc: \<DOC>
Default `make-keep-out` - No Keep-out Regions Applied.
<DOC>
public defmethod make-keep-out (
  pkg:Package
  --
  pose:Pose = DEF_LP_POSE
  density-level:DensityLevel = DENSITY-LEVEL
  ):
  inside pcb-landpattern:
    val virt = VirtualLP(pose)
    build-keep-out(pkg, virt, density-level = density-level)
    make-landpattern(virt)

doc: \<DOC>
Create conformal coat mask regions in a virtual landpattern

This function is used to build the landpattern's
conformal coat mask regions in a virtual landpattern environment.
Typically this is used to mark where on this component conformal
coating should not be applied - like for connector openings, etc.
That virtual environment can then be written out to a
`pcb-landpattern` definition.

@param pkg Package definition to build
@param vp Virtual Landpattern Container
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti build-conformal-mask (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = ?
  ) -> False

doc: \<DOC>
Default `build-conformal-mask` - No Conformal Mask Regions Applied.
<DOC>
public defmethod build-conformal-mask (
  pkg:Package,
  vp:VirtualLP,
  --
  density-level:DensityLevel = DENSITY-LEVEL
  ) -> False:
  false

doc: \<DOC>
Generator for creating conformal coat masks for a landpattern

This generator is typically called within a `pcb-landpattern`
definition to create the conformal coat mask regions. This is
typically useful for connectors or thermal interfaces.

@param pkg IC Package for which we are creating silkscreen artwork
@param pkg pose Offset to apply to all of the geometry generated
by this generator function. By default, no offset is applied.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti make-conformal-mask (
  pkg:Package
  --
  pose:Pose = ?
  density-level:DensityLevel = ?
  )

doc: \<DOC>
Default Conformal Coat Generator
<DOC>
public defmethod make-conformal-mask (
  pkg:Package
  --
  pose:Pose = DEF_LP_POSE
  density-level:DensityLevel = DENSITY-LEVEL
  ):
  inside pcb-landpattern:
    val virt = VirtualLP(pose)
    build-conformal-mask(pkg, virt, density-level = density-level)
    make-landpattern(virt)


doc: \<DOC>
Apply all `Package` builder functions

This function applies all of the `build-*` functions
associated with the `Package` type to a virtual landpattern container.

@param pkg IC Package for which we are creating silkscreen artwork
@param virt Virtual Landpattern to which content will be written
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmulti build-all (
  pkg:Package,
  virt:VirtualLP
  --
  density-level:DensityLevel = ?
  ) -> False

doc: \<DOC>
Default `build-all` implementation

Builders are applied in the following order:

1.  {@link build-landpattern}
1.  {@link build-courtyard}
1.  {@link build-silkscreen}
1.  {@link build-keep-out}
1.  {@link build-conformal-mask}

<DOC>
public defmethod build-all (
  pkg:Package,
  virt:VirtualLP
  --
  density-level:DensityLevel = DENSITY-LEVEL
  ) -> False :
  build-landpattern(
    pkg,
    virt,
    density-level = density-level
    )
  build-courtyard(
    pkg,
    virt,
    density-level = density-level
    )
  build-silkscreen(
    pkg,
    virt,
    density-level = density-level
    )
  build-keep-out(
    pkg,
    virt,
    density-level = density-level
    )
  build-conformal-mask(
    pkg,
    virt,
    density-level = density-level
    )


doc: \<DOC>
Generator for applying all `Package` generators

This function applies all of the `make-*` generators
associated with the `Package` type.

@param pkg IC Package for which we are creating silkscreen artwork
@param pkg pose Offset to apply to all of the geometry generated
by this generator function. By default, no offset is applied.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.

<DOC>
public defmulti make-all (
  pkg:Package
  --
  pose:Pose = ?
  density-level:DensityLevel = ?
  )

doc: \<DOC>
Default `make-all` generator implementation

@param pkg IC Package for which we are creating silkscreen artwork
@param pkg pose Offset to apply to all of the geometry generated
by this generator function. By default, no offset is applied.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmethod make-all (
  pkg:Package
  --
  pose:Pose = DEF_LP_POSE
  density-level:DensityLevel = DENSITY-LEVEL
  ):
  inside pcb-landpattern:
    val virt = VirtualLP(pose)
    build-all(pkg, virt, density-level = density-level)
    make-landpattern(virt)

doc: \<DOC>
Create a `LandPattern` definition

This function is used to create a complete landpattern definition
for this `Package`. Typically this function would use the `make-*`
generators for this type to construct a `pcb-landpattern` definition
and return it.

@param pkg Package to create a landpattern for.
@param pose Geometry gets created with an offset from the origin
by `pose`.
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.

Example:
```
  val lp = create-landpattern(pkg)

  ; Is equivalent to:

  pcb-landpattern lp:
    name = "lp"
    description = ""
    make-landpattern(pkg)
```
<DOC>
public defmulti create-landpattern (
  pkg:Package
  --
  pose:Pose = ?,
  description:String = ?,
  density-level:DensityLevel = ?
  ) -> LandPattern

doc: \<DOC>
Default Implementation of `create-landpattern`

The default landpattern creator combines all of the
defined generators for a `Package` (ie, `make-*` functions)
to generate the complete landpattern for a package.

In more complex packages, you may need to define your own
`pcb-landpattern` definition with multiple packages

@param pkg Package to create a landpattern for.
@param pose Offset of the geometry in the landpattern. Default
is no offset, ie `loc(0.0, 0.0)`
@param density-level Density Level for this LandPattern definition.
See IPC7351 regarding Maximum, Nominal, and Least Material Conditions.
<DOC>
public defmethod create-landpattern (
  pkg:Package
  --
  pose:Pose = DEF_LP_POSE,
  description:String = "",
  density-level:DensityLevel = DENSITY-LEVEL
  ) -> LandPattern :
  pcb-landpattern lp:
    name = name(pkg)
    make-all(pkg, pose = pose, density-level = density-level)
  lp




