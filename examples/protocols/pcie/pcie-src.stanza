#use-added-syntax(jitx)
defpackage jsl/examples/protocols/pcie/pcie-src :
  import core
  import jitx
  import jitx/commands

  import jsl

val BGA-pkg = BGA(
  num-leads = 64,
  rows = 8,
  columns = 8,
  lead-diam = 0.5,
  pitch = 2.0,
  package-body = PackageBody(
    width = 15.0 +/- 0.1,
    length = 15.0 +/- 0.1,
    height = 0.71 +/- [0.06, 0.0]
  ),
  density-level = DensityLevelB
)


public pcb-component component :
  reference-prefix = "U"
  mpn = "JITX001"
  description = "Dummy device with multiple PCIe supports"

  port REFCLK : diff-pair[4]

  pin-properties :
    [pin:Ref    | pads:Ref ... ]

    [REFCLK[0].N | A[1] ]
    [REFCLK[0].P | A[2] ]
    [REFCLK[1].P | D[4] ]
    [REFCLK[1].N | D[3] ]
    [REFCLK[2].P | D[2] ]
    [REFCLK[2].N | D[1] ]
    [REFCLK[3].P | C[7] ]
    [REFCLK[3].N | C[6] ]

    [PTXP[0]    | B[3] ]
    [PTXN[0]    | B[2] ]
    [PTXP[1]    | A[8] ]
    [PTXN[1]    | A[7] ]
    [PTXP[2]    | C[4] ]
    [PTXN[2]    | C[3] ]
    [PTXP[3]    | C[2] ]
    [PTXN[3]    | C[1] ]

    [PRXP[0]    | A[6] ]
    [PRXN[0]    | A[5] ]
    [PRXP[1]    | A[4] ]
    [PRXN[1]    | A[3] ]
    [PRXP[2]    | B[7] ]
    [PRXN[2]    | B[6] ]
    [PRXP[3]    | B[5] ]
    [PRXN[3]    | B[4] ]

    [PERST#[0]  | C[5] ]
    [PERST#[3]  | D[5] ]
    [PERST#[2]  | D[6] ]
    [PERST#[1]  | D[7] ]

    [GPIO[1]    | E[1] ]
    [GPIO[2]    | E[2] ]
    [GPIO[3]    | E[3] ]
    [GPIO[4]    | E[4] ]
    [GPIO[5]    | E[5] ]
    [GPIO[6]    | E[6] ]
    [GPIO[7]    | E[7] ]
    [GPIO[8]    | F[1] ]
    [GPIO[9]    | F[2] ]
    [GPIO[10]   | F[3] ]
    [GPIO[11]   | F[4] ]
    [GPIO[12]   | F[5] ]
    [GPIO[13]   | F[6] ]
    [GPIO[14]   | F[7] ]
    [GPIO[15]   | G[1] ]
    [GPIO[16]   | G[2] ]
    [GPIO[17]   | G[3] ]
    [GPIO[18]   | G[4] ]
    [GPIO[19]   | G[5] ]
    [GPIO[20]   | G[6] ]
    [GPIO[21]   | G[7] ]

  val bus-r = Ref("bus")
  val ctl-r = Ref("ctl")

  val box-params = BoxSymbolParams(
    show-grid = false
    min-col-width = 1
  )

  val box = BoxSymbol(self, params = box-params)
  set-grid(box, [2, 2], ctl-r)

  set-bank(bus-r, self.PRXP, self.PRXN)
  set-bank(bus-r, self.PTXP, self.PTXN)

  set-group("tx-grp", self.PTXP, self.PTXN)
  set-group("rx-grp", self.PRXP, self.PRXN)
  set-head-margin(box, 0.5, "rx-grp")

  set-side(Right, self.PTXP, self.PTXN, self.PRXP, self.PRXN)

  set-bank(ctl-r, self.GPIO, self.PERST#)
  set-bank(ctl-r, self.REFCLK)

  set-group("reset-grp", self.PERST#)

  set-group("gpio-grp", self.GPIO)
  set-head-margin(box, 0.5, "gpio-grp")

  set-side(Left, self.GPIO, self.PERST#)

  for i in 0 to 4 do:
    val  name = to-string $ "ref-clk-%_" % [i]
    set-group(name, self.REFCLK[i].P, self.REFCLK[i].N )
    set-head-margin(box, 0.5, name)

  set-side(Right, self.REFCLK)

  assign-symbols(
    ctl-r => box,
    bus-r => box
  )

  val lp = create-landpattern(BGA-pkg)
  assign-landpattern(lp)


  for i in 0 to 4 do :
    diff-pin-model(self.PRXP[i], self.PRXN[i],       delay = typ(10.0e-15) loss = typ(0.1))
    diff-pin-model(self.PTXP[i], self.PTXN[i],       delay = typ(10.0e-15) loss = typ(0.1))
    diff-pin-model(self.REFCLK[i].P, self.REFCLK[i].N, delay = typ(10.0e-15) loss = typ(0.1))



defn connect_lanes (
  b:Bundle,
  ref_clk:JITXObject,
  ctl_lines:[JITXObject, JITXObject, JITXObject],
  present:JITXObject|False,
  --
  ch-offset:Int = 0
  ):
  val [wake, reset, clkreq] = ctl_lines
  inside pcb-module:
    supports b:
      for i in indices(b.data.lane) do :
        b.data.lane[i].RX.P => self.sw.PRXP[i + ch-offset]
        b.data.lane[i].RX.N => self.sw.PRXN[i + ch-offset]
        b.data.lane[i].TX.P => self.sw.PTXP[i + ch-offset]
        b.data.lane[i].TX.N => self.sw.PTXN[i + ch-offset]
      b.data.refclk.P => ref_clk.P
      b.data.refclk.N => ref_clk.N

      if has-PEWAKE#(b):
        b.control.PEWAKE# => wake
      if has-PERST#(b):
        b.control.PERST#  => reset
      if has-CLKREQ#(b):
        b.control.CLKREQ# => clkreq

      if has-PRSNT#(b):
        match(present):
          (_:False):
            throw $ ValueError("Missing Present Connection - Passed False")
          (p:JITXObject):
            b.control.PRSNT#  => p


public pcb-module module :

  public inst sw : component

  ; Optional Connection to demonstrate shared
  ;  pins across pin assigned bundles.
  port shared_wake

  net (shared_wake, sw.GPIO[7])

  ;;;;;;;;;;;;;;;;;;;;;;;
  ; PCIe 4x
  ;;;;;;;;;;;;;;;;;;;;;;;


  val bd-4x = pcie-std(4)
  connect_lanes(bd-4x, self.sw.REFCLK[0], [sw.GPIO[8], sw.PERST#[0], sw.GPIO[1]], false)

  val bd-4x-prsnt = pcie-with-hotplug(4)
  connect_lanes(bd-4x-prsnt, self.sw.REFCLK[0], [sw.GPIO[8], sw.PERST#[0], sw.GPIO[1]], sw.GPIO[12])

  ;;;;;;;;;;;;;;;;;;;;;;;
  ; PCIe 1x
  ;;;;;;;;;;;;;;;;;;;;;;;

  val bd-1x = pcie-std(1)
  val bd-1x-prsnt = pcie-with-hotplug(1)

  val ctls-1x = [
    [sw.GPIO[8], sw.PERST#[0], sw.GPIO[1]],
    [sw.GPIO[9], sw.PERST#[1], sw.GPIO[3]],
    [sw.GPIO[10], sw.PERST#[2], sw.GPIO[2]],
    [sw.GPIO[11], sw.PERST#[3], sw.GPIO[4]],
  ]

  for j in 0 to 4 do:
    connect_lanes(bd-1x, self.sw.REFCLK[j], ctls-1x[j], false, ch-offset = j)
    connect_lanes(bd-1x-prsnt, self.sw.REFCLK[j], ctls-1x[j], sw.GPIO[12 + j], ch-offset = j)


  ;;;;;;;;;;;;;;;;;;;;;;;
  ; PCIe 2x
  ;;;;;;;;;;;;;;;;;;;;;;;

  val pcie-2x-shared-wake = sw.GPIO[7]
  val ctls-2x = [
    [sw.GPIO[8], sw.PERST#[0], sw.GPIO[1]]
    [sw.GPIO[10], sw.PERST#[2], sw.GPIO[3]]
  ]
  ; The internal bundle contains reset, and clock request
  ;  but does not include the WAKE
  val bd-2x = pcie-std(2)
  val bd-2x-prsnt = pcie-with-hotplug(2)

  for j in 0 to 2 do:
    connect_lanes(bd-2x, self.sw.REFCLK[j], ctls-2x[j], false, ch-offset = 2 * j)
    connect_lanes(bd-2x-prsnt, self.sw.REFCLK[j], ctls-2x[j], sw.GPIO[12 + j], ch-offset = 2 * j)


