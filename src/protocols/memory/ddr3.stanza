doc: \<DOC>
@brief Double Data Rate 3 Memory Standard (DDR3)

DDR3 is a high speed memory protocol
@see https://en.wikipedia.org/wiki/DDR3_SDRAM

This functions and definitions in this file support defining DDR3
connections between microprocessors and memories in a board design.

This file contains curated values for skew and loss of DDR3 channel
It specifies the bounds on the intra-pair
skew timing and maximum loss as expected by this particular standard. The values
returned are a toleranced value with upper/lower limits for the
intra-pair skew and the maximum loss as a double representing dB. Some defaults in the
table are derived from the references listed below.

1. Skew match between CMD/ADDR/CTRL and CK.P is 0.0 +/- 25e-12 (25 ps)
2. Skew match of CK is 0.0 +/- 1.0e-12 (1 ps)
3. Skew match of DQS is 0.0 +/- 1.0e-12 (1 ps)
4. Skew match between DQ and DQS.P within a byte lane is 0.0 +/- 3.5e-12 (3.5 ps)
5. Skew match between CK.P and DQS.P is -75 ps to 900 ps (example values, needs verification)

<DOC>

#use-added-syntax(jitx)
defpackage jsl/protocols/memory/ddr3:
  import core
  import jitx
  import jitx/commands

  import jsl/bundles
  import jsl/errors
  import jsl/ensure
  import jsl/si
  import jsl/pin-assignment

doc: \<DOC>
@brief DDR3 width enums
This is a fixed list of possible lane widths for DDR3 memory channels
@member DDR3-x8   8 bit width
@member DDR3-x16 16 bit width
<DOC>
public pcb-enum jsl/protocols/memory/ddr3/DDR3-width:
  DDR3-x8
  DDR3-x16

doc: \<DOC>
@brief DDR3 Width enum conversion to integers
This converts possible lane widths for ddr3 memory channels into the corresponding integer
<DOC>
public defn width-to-int (en:DDR3-width) -> Int:
  switch(en) :
    DDR3-x8  :  8
    DDR3-x16 : 16

doc: \<DOC>
@brief DDR3 Width enum conversion to lane count
This converts possible lane widths for ddr3 memory channels
into the corresponding integer number of 8-bit lanes which
determines the number of DQS pairs required.
<DOC>
public defn width-to-lane (en:DDR3-width) -> Int:
  switch(en) :
    DDR3-x8  : 1
    DDR3-x16 : 2

doc: \<DOC>
@brief ddr3 topology
This is a fixed list of possible topologies for ddr3 memories
@member FlyBy
@member ClamShell
<DOC>
public pcb-enum jsl/protocols/memory/ddr3/DDR3-topology:
  FlyBy
  ClamShell

doc: \<DOC>
@brief DDR3 rank Options
This is a fixed list of possible rank options for ddr3 implementations
@member SingleRank
@member DualRank
@member QuadRank
<DOC>
public pcb-enum jsl/protocols/memory/ddr3/DDR3-rank:
  SingleRank
  DualRank

doc: \<DOC>
@brief DDR3 rank enum conversion to integers
This converts possible rank options for ddr3 memory channels into a corresponding integer
which determines the number of CS_n signals needed to control the memories
<DOC>
public defn rank-to-int (en:DDR3-rank) -> Int:
  switch(en) :
    SingleRank : 1
    DualRank   : 2

doc: \<DOC>
@brief ddr3 Bundle
One ddr3 bundle consists of the connections between an integrated memory controller and
one or more ddr3 memory chips/DIMMs.
@member data Data channel(s) for ddr3 depending on the width
@member ctl Control channel for ddr3
@member rank Rank configuration of the DDR3 memory (SingleRank, DualRank, or QuadRank)
@member topology Topology of the DDR3 connection (FlyBy or ClamShell)
<DOC>

public defn ddr3 (width:DDR3-width -- rank:DDR3-rank = SingleRank, topology:DDR3-topology = FlyBy) :
  ddr3-b(width, rank, topology)

public pcb-bundle ddr3-b (width:DDR3-width, rank:DDR3-rank, topology:DDR3-topology) :
  name = "DDR3"
  description = "DDR3 Dynamic Random-Access Memory protocol"
  port data    : ddr3-data-channel(width) ;
  port ctl     : ddr3-ctl-channel(rank) ; covers all CMD/CTRL/ADDR

public pcb-bundle ddr3-data-channel (width:DDR3-width):
  port DQ    : pin[width-to-int(width)] ; will vary from 3:0 to 7:0 to 15:0 to 71:0 as appropriate
  port DQS   : diff-pair[width-to-lane(width)] ; will either be 0 or 1:0 to 8:0
  port DM    : pin[width-to-lane(width)] ; data mask per 8b lane (note: DDR3 uses DM, not DM_n)

public pcb-bundle ddr3-ctl-channel (rank:DDR3-rank):
  port CK      : diff-pair
  port CKE     : pin
  port A       : pin[15]  ; DDR3 typically has 15 address lines compared to 17 in DDR4
  port RAS_n   : pin      ; DDR3 has separate RAS_n, CAS_n, WE_n unlike DDR4
  port CAS_n   : pin
  port WE_n    : pin
  port BA      : pin[3]   ; DDR3 has 3 bank address lines (no BG)
  port CS_n    : pin[rank-to-int(rank)]
  port RESET_n : pin
  port ODT     : pin

doc: \<DOC>
@brief Differential impedance specified by the DDR3 standard
This is a helper function that returns the expected differential
trace impedance for the standard.

@return Upper/lower limits for the impedance in a struct.
<DOC>
public defstruct DDR3-Impedances <: Equalable:
  ck-imped:Toleranced with:
     default => 90.0 +/- (5 %)
  dqs-imped:Toleranced with:
     default => 100.0 +/- (5 %)
  dq-imped:Toleranced with:
    default => 50.0 +/- (5 %)
  acc-imped:Toleranced with:
    default => 45.0 +/- (5 %)
with:
  printer => true
  keyword-constructor => true
  equalable => true

doc: \<DOC>
DDR3 SI Constraint Type

This derives from the whole data lane constraint as most of the dq signals
are single-ended but referenced to the dqs pairs. The remainder of the signals are
constrained to the CK pair. The constraints are:

    0.0 +/-    1.0e-12   ; Intra-pair skew match of CK is +/- 1 ps
    0.0 +/-    1.0e-12   ; Intra-pair skew match of DQS is +/- 1 ps
  -75.0e-12 +900.0e-12   ; Skew match between CK.P and DQS.P -75 ps to 900 ps (example values, needs verification)
    0.0 +/-    3.5e-12   ; Skew match between DQ and DQS.P within a byte lane is +/- 3.5 ps
    0.0 +/-   25.0e-12   ; Skew match between CMD/ADDR/CTRL and CK.P +/- 25 ps

The `constrain` function for this type expects two compatible `ddr3-b` types.
    ck-rs:DifferentialRoutingStructure
    dqs-rs:DifferentialRoutingStructure
    dq-rs:RoutingStructure
    rs:RoutingStructure ) -> DDR3-Constraint:

<DOC>
public defstruct DDR3-Constraint <: SI-Constraint :
  doc: \<DOC>
  Channel Bit Width
  <DOC>
  width:DDR3-width
  doc: \<DOC>
  Topology of memory channel
  <DOC>
  topology:DDR3-topology
  doc: \<DOC>
  Channel Rank
  <DOC>
  rank:DDR3-rank
  doc: \<DOC>
  CK Intra-pair Timing Skew Constraint in Seconds
  <DOC>
  skew-ck:Toleranced with :
    default => 0.0 +/- 1.00e-12
  doc: \<DOC>
  DQS Intra-pair Timing Skew Constraint in Seconds
  <DOC>
  skew-dqs:Toleranced with :
    default => 0.0 +/- 1.00e-12
  doc: \<DOC>
  CK.P to DQS.P Inter-signal Timing Skew Constraint in Seconds
  <DOC>
  skew-ck-dqs:Toleranced with :
    default => min-typ-max(-75.0e-12, 0.0, 900.0e-12)
  doc: \<DOC>
  DQ to DQS Inter-pair Timing Skew Constraint in Seconds
  <DOC>
  skew-dq-dqs:Toleranced with :
    default => 0.0 +/- 3.50e-12
  doc: \<DOC>
  DM to DQS.P Timing Skew Constraint in Seconds
  <DOC>
  skew-dm-dqs:Toleranced with:
    default => 0.0 +/- 3.50e-12 ; Same as DQ timing
  doc: \<DOC>
  CMD/ADDR/CTRL to CK Inter-pair Timing Skew Constraint in Seconds
  <DOC>
  skew-cmd-addr-ctrl-ck:Toleranced with :
    default => 0.0 +/- 25.0e-12  ; DDR3 spec is 25ps for control signals to clock timing
  doc: \<DOC>
  CMD, ADDR, CTRL (w/o CK) Intra-group Timing Skew Constraint in Seconds
  <DOC>
  skew-cmd-addr-ctrl:Toleranced  with:
    default =>  0.0 +/- 10.0e-12
  doc: \<DOC>
  Diff-Pair Max Loss Limit Constraint in dB
  <DOC>
  loss:Double with:
    ensure => ensure-positive!,
    default => 5.0
  doc: \<DOC>
  Differential Routing Structure for CK  [90.0 +/- (5 %)]
  <DOC>
  diff-ck-rs:DifferentialRoutingStructure
  doc: \<DOC>
  Differential Routing Structure for DQS [100.0 +/- (5 %)]
  <DOC>
  diff-dqs-rs:DifferentialRoutingStructure
  doc: \<DOC>
  Single-Ended Routing Structure for each DQ signal [50.0 +/- (5 %)]
  <DOC>
  se-dq-rs:RoutingStructure
  doc: \<DOC>
  Single-Ended Routing Structure for all other SE signal [45.0 +/- (5 %)]
  <DOC>
  se-rs:RoutingStructure

with:
  keyword-constructor => true
  constructor => #DDR3-Constraint


doc: \<DOC>
Constructor for the DDR3 Link Constraint
@param ck-rs Differential Routing Structure constraints for CK signals
@param dqs-rs Differential Routing Structure constraints for DQS signals
@param dq-rs Routing Structure constraints for DQ signals
@param rs Routing Structure constraints for remaining signals (CMD/CTRL/ADDR)
<DOC>
public defn DDR3-Constraint (
    width:DDR3-width,
    topology:DDR3-topology,
    rank:DDR3-rank,
    diff-ck-rs:DifferentialRoutingStructure,
    diff-dqs-rs:DifferentialRoutingStructure,
    se-dq-rs:RoutingStructure,
    se-rs:RoutingStructure ) -> DDR3-Constraint:
  DDR3-Constraint(
    width = width,
    topology = topology,
    rank = rank,
    diff-ck-rs = diff-ck-rs,
    diff-dqs-rs = diff-dqs-rs,
    se-dq-rs = se-dq-rs,
    se-rs = se-rs
  )

doc: \<DOC>
Constrain a DDR3 Link assuming that it is a discrete link (not DIMM)

@param cst Constraint Object
@param src Source End Point - must be of `ddr3-b` type
@param dst Destination End Point - must be of `ddr3-b` type and match
the parameterization of `src`, including width.
<DOC>
public defmethod constrain (cst:DDR3-Constraint, src:JITXObject, dst:JITXObject) -> False :
  inside pcb-module:

    val ilane  = width-to-lane(width(cst))
    val irank  = rank-to-int(rank(cst))

    val cst-ck  = DiffPair-Constraint(skew = skew-ck(cst), loss = loss(cst), route-struct = diff-ck-rs(cst))
    val cst-dqs = DiffPair-Constraint(skew = skew-dqs(cst), loss = loss(cst), route-struct = diff-dqs-rs(cst))

    val guide-ck  = src.ctl.CK.P => dst.ctl.CK.P
    constrain(cst-ck, src.ctl.CK, dst.ctl.CK)

    ; ex. for x8,  i goes from 0 to 1, so all of the byte lanes are individually constrained
    ; ex. for x16, i goes from 0 to 2, so all of the byte lanes are individually constrained
    for i in 0 to ilane do :
      val guide-dqs = src.data.DQS[i].P => dst.data.DQS[i].P
      constrain(cst-dqs, src.data.DQS[i] dst.data.DQS[i])
      ; constrain DQS with respect to CK
      timing-window(skew-ck-dqs(cst), guide-ck, guide-dqs)
      ; constrain DQ with respect to DQS
      val bs = i * 8
      val be = bs + 8
      val target-dqm-bus = to-tuple $ cat{_, [src.data.DM[i] => dst.data.DM[i]]} $ for j in bs to be seq : src.data.DQ[j] => dst.data.DQ[j]
      timing-window(skew-dq-dqs(cst), guide-dqs, topos = target-dqm-bus)
      max-loss(loss(cst), ep-sets = target-dqm-bus)
    ; needs to be outside the loop as structure() can't handle a Tuple<KeyValue> argument
    structure(src.data.DQ, dst.data.DQ) = se-dq-rs(cst)
    structure(src.data.DM, dst.data.DM) = se-dq-rs(cst)

    ; now constrain addr/cmd/ctrl signals (1x per bus)
    ; there are two constraints to be applied:
    ; First is versus CK and is +/- 25 ps for DDR3
    ; Second is versus the other signals in the same group and is +/- 10 ps
    ; let's pick an arbitrary signal to serve as the guide for the overall ADDR/CMD/CTL group
    val guide-acc-group = src.ctl.CKE => dst.ctl.CKE
    ; now back to our regularly scheduled program
    val target-cke = src.ctl.CKE => dst.ctl.CKE
    timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-cke)
    ; no need for timing-window for the CKE signal versus guide-acc-group as it's defining the group timing constraint
    structure(target-cke) = se-rs(cst)
    max-loss(loss(cst), target-cke)
    for j in 0 to 15 do :  ; DDR3 has 15 address lines
      val target-addr = src.ctl.A[j] => dst.ctl.A[j]
      timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-addr)
      timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-addr)
      structure(target-addr) = se-rs(cst)
      max-loss(loss(cst), target-addr)

    val target-ras = src.ctl.RAS_n => dst.ctl.RAS_n
    timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-ras)
    timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-ras)
    structure(target-ras) = se-rs(cst)
    max-loss(loss(cst), target-ras)
    
    val target-cas = src.ctl.CAS_n => dst.ctl.CAS_n
    timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-cas)
    timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-cas)
    structure(target-cas) = se-rs(cst)
    max-loss(loss(cst), target-cas)
    
    val target-we = src.ctl.WE_n => dst.ctl.WE_n
    timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-we)
    timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-we)
    structure(target-we) = se-rs(cst)
    max-loss(loss(cst), target-we)
    
    for j in 0 to 3 do :  ; DDR3 has 3 bank address lines
      val target-ba = src.ctl.BA[j] => dst.ctl.BA[j]
      timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-ba)
      timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-ba)
      structure(target-ba) = se-rs(cst)
      max-loss(loss(cst), target-ba)
    
    for j in 0 to irank do:
      val target-cs = src.ctl.CS_n[j] => dst.ctl.CS_n[j]
      timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-cs)
      timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-cs)
      structure(target-cs) = se-rs(cst)
      max-loss(loss(cst), target-cs)
    
    val target-reset = src.ctl.RESET_n => dst.ctl.RESET_n
    timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-reset)
    timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-reset)
    structure(target-reset) = se-rs(cst)
    max-loss(loss(cst), target-reset)
    
    val target-odt = src.ctl.ODT => dst.ctl.ODT
    timing-window(skew-cmd-addr-ctrl-ck(cst), guide-ck, target-odt)
    timing-window(skew-cmd-addr-ctrl(cst), guide-acc-group, target-odt)
    structure(target-odt) = se-rs(cst)
    max-loss(loss(cst), target-odt)


public defn connect-DDR3 (src:JITXObject, dst:JITXObject
                          -- width:DDR3-width = DDR3-x16,
                          topology:DDR3-topology = FlyBy,
                          rank:DDR3-rank = SingleRank,
                          diff-ck-rs:DifferentialRoutingStructure,
                          diff-dqs-rs:DifferentialRoutingStructure,
                          dq-rs:RoutingStructure,
                          rs:RoutingStructure) :
  inside pcb-module :
    val ddr3-cst = DDR3-Constraint(width = width,
                                   topology = topology,
                                   rank = rank,
                                   diff-ck-rs = diff-ck-rs,
                                   diff-dqs-rs = diff-dqs-rs,
                                   se-dq-rs = dq-rs,
                                   se-rs = rs)
    constrain-topology(src, dst, ddr3-cst) 